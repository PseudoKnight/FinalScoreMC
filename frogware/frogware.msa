*:/frogware [$cmd='usage'] [$pts=25] = >>>
	switch($cmd
	, 'join',
		if(!sk_region_exists('custom', 'frogware'), die('Define the frogware region first.'))
		if(!array_contains(get_scoreboards(), 'fw')) {
			create_scoreboard('fw')
			create_objective('score', 'DUMMY', 'fw')
			create_team('losers', 'fw')
			create_team('winners', 'fw')
			set_objective_display('score', array('slot': 'SIDEBAR', 'displayname': color('a').color('l').'Get Ready!'), 'fw')
			set_team_display('winners', array('prefix': color(10)), 'fw')
			set_team_display('losers', array('prefix': color(15)), 'fw')
		}
		set_pscoreboard(player(), 'fw')
		team_add_player('losers', player(), 'fw')
		include('includes.library/frogware.ms')
		
		@scores = array()
		foreach(all_players('custom'), @p,
			if(_fw_player(@p)) {
				array_push(get_pscore('score', @p, 'fw'))
			}
		)
		set_pscore('score', player(), if(@scores, min(@scores), 0), 'fw')
		
		set_ploc(_fw_loc(-1))
		if(pmode() == 'CREATIVE', set_pmode('SURVIVAL'))
		set_phunger(20)
		set_psaturation(5)
		set_phealth(20)
		_clear_pinv(player())
		
	, 'start',
		if(get_pscoreboard(player()) != 'fw', die('You haven\'t joined first.'))
		if(!is_numeric($pts), die('You must specify the point goal as a number. eg. /frogware start 25'))
		include('includes.library/frogware.ms')
		foreach(all_players('custom'), @p,
			if(_fw_player(@p)) {
				set_phunger(@p, 20)
				set_psaturation(@p, 5)
				set_phealth(@p, 20)
				_clear_pinv(@p)
			}
		)
		_fw_startgame($pts)
	
	, 'forcestop',
		queue_clear('fw')
		queue_clear('fw2')
		queue_clear('fw3')
		remove_scoreboard('fw')
		unbind('fwdamage')
	
	,
		msg('| FrogWare is a game inspired by GarryWare,\n'
		.'| which is in turn inspired by WarioWare.\n'
		.'| Players get a random task every round that\n'
		.'| they have to complete in several seconds.\n'
		.'| They get a point for completing a task.\n'
		.'| First player to reach 25 points wins.')
	)
<<<