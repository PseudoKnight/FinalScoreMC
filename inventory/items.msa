# Add an item into your inventory in the next available slot.
*:/i [$item=0] [$amount=1] = >>>
	if(!has_permission('command.items')) {
		die('You are not allowed to create custom items here.');
	}
	if($item == 0) {
		die('Usage: /i [id:data] [quantity] '.color('gray').'(supports item names)');
	}
	include('util.library/items.ms');
	_pgive_item($item, $amount);
<<<

# Set the display name for the item in-hand
*:/display [$] = >>>
	if(!has_permission('command.items')) {
		die('You are not allowed to create custom items here.');
	}
	if(!$) {
		die(color('gold').'Usage: /display <item display name> '.color('gray').'(supports &0 color codes)');
	}
	@meta = get_itemmeta(null);
	if(is_null(@meta)) {
		@meta = associative_array();
	}
	@meta['display'] = colorize($);
	set_itemmeta(null, @meta);
<<<

# Enchant tool in hand with specified enchantment
*:/enchant [$type='list'] [$level=1] = >>>
	if(!has_permission('command.items')) {
		die('You are not allowed to create custom items here.');
	}
	if(!$type) {
		die(color('gold').'Usage: /enchant <type> [level]\n'
		.'Example: /enchant sharpness 4');
	}
	@item = pinv(player(), null);
	if(!@item) {
		die('You must hold an item.');
	}
	include('util.library/items.ms');
	_enchant_item($type, $level);
<<<

# Remove all enchantments from item in hand
*:/unenchant = >>>
	if(!has_permission('command.items')) {
		die('You are not allowed to create custom items here.');
	}
	@item = pinv(player(), null);
	array_remove(@item, 'enchants');
	array_remove(@item['meta'], 'enchants');
	set_pinv(array(null: @item));
<<<

# Set individual lines of lore for the item in-hand
*:/lore [$line] [$] = >>>
	if(!has_permission('command.items')) {
		die('You are not allowed to create custom items here.');
	}
	if(!$ || !is_numeric($line) || $line < 1) {
		die(color('gold').'Usage: /lore <line#> <lore string> '.color('gray').'(supports &0 color codes)');
	}
	@meta = get_itemmeta(null);
	if(is_null(@meta)) {
		@meta = associative_array('lore': array());
	} else if(is_null(@meta['lore'])) {
		@meta['lore'] = array();
	}
	@meta['lore'][$line - 1] = colorize($);
	set_itemmeta(null, @meta);
<<<

# Spawn player head
*:/skull [$player] = >>>
	if(!has_permission('command.items')) {
		die('You are not allowed to create custom items here.');
	}
	if($player) {
		pgive_item(player(), '397:3', 1, array('owner': $player));
	} else {
		msg(color('gray').'Usage: '.color('r').'/skull <player>');
	}
<<<

# Dye leather armor in-hand with the specified R G B values (0 - 255)
*:/dye [$r] [$g] [$b] = >>>
	if(!has_permission('command.items')) {
		die('You are not allowed to create custom items here.');
	}
	if(!$b) {
		die(color('gold').'Usage: /dye <red#> <green#> <blue#> '.color('gray').'(0 - 255)');
	}
	try {
		set_armor_color(pinfo(player(), 15), array(min(max($r, 0), 255), min(max($g, 0), 255), min(max($b, 0), 255)));
	} catch(Exception @ex) {
		die(color('gold').'Hold leather armor in your hand and type the command. eg. /dye 255 255 255');
	}
<<<

# Create potions or add potion effects to existing potions
*:/potion [$type] [$id] [$seconds = 30] [$strength = 1] = >>>
	if(!has_permission('command.items')) {
		die('You are not allowed to create custom items here.');
	}
	if(!$type) {
		die('Usage: /potion <potionType> <potionEffect> [seconds] [strength]');
	}
	include('util.library/items.ms');
	_pset_potion($type, $id, $seconds, $strength);
<<<
