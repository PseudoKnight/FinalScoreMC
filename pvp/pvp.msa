*:/pvp [$action] [$id] [$team] = >>>
	include('core.library/main.ms')
	switch($action) {
	case 'join':
		@id = to_lower($id)
		@pvp = import('pvp'.@id)
		@arena = get_value('arena.'.@id)
		if(!@arena, die('Can\'t find that arena.'))
		if(!@pvp) {
			@pvp = array('players': array(), 'running': 0)
			@pvp['players'][player()] = array()
			export('pvp'.@id, @pvp)
		} else {
			if(array_index_exists(@pvp['players'], player()), die('You already joined.'))
			if(@pvp['running'] > 1 && 
			(array_index_exists(@pvp['arena'], 'lives')
			|| (array_index_exists(@pvp['arena'], 'max') && array_size(@pvp['players']) >= @pvp['arena']['max']))) {
				die('Match already in progress.')
			}
			@pvp['players'][player()] = array()
		}
		if(phas_flight(),
			set_pflight(false))
		foreach(@player in array_keys(@pvp['players'])) {
			if(!ponline(@player)) {
				array_remove(@pvp['players'], @player)
			}
		}
		_worldmsg('custom', color('gray').'[PVP] '.color('r').player().' has joined '.to_upper(if(array_index_exists(@arena, 'parent'), @arena['parent'].': ').@id).'. ('.array_size(@pvp['players']).')')
		console(player().' joined '.@id)
		if(@pvp['running'] == 1) {
			if(array_index_exists(@pvp['arena'], 'team')) {
				_assign_team(player(), @pvp)
			}
			if(array_index_exists(@pvp['arena'], 'classes')) {
				if(!array_index_exists(@pvp['players'][player()], 'team') || @pvp['players'][player()]['team'] == 0) {
					if(array_contains(all_virtualchests(), @id.'0')) {
						popen_virtualchest(player(), @id.'0')
					}
				} else {
					if(array_contains(all_virtualchests(), @id.'1')) {
						popen_virtualchest(player(), @id.'1')
					}
				}
			}
			
		} else if(@pvp['running'] > 1) {
			if(array_index_exists(@pvp['arena'], 'resourcepack')) {
				send_resourcepack(player(), 'http://mc.finalscoremc.com:25966/resourcepacks/'.@pvp['arena']['resourcepack'].'.zip')
			}
			if(array_index_exists(@pvp['arena'], 'team')) {
				_assign_team(player(), @pvp)
			}
			if(array_contains(@pvp['spectators'], player())) {
				_remove_spectator(@id, player(), @pvp)
			}
			_pvp_initialize_players(@id, array(player()))
			
		} else if(array_index_exists(@arena, 'max') && array_size(@pvp['players']) >= @arena['max']) {
			call_alias('/pvp start '.@id)
		}
		
	case 'debug':
		@id = to_lower($id)
		@pvp = import('pvp'.@id)
		if(is_null(@pvp)) {
			die('That arena doesn\'t seem to be running.')
		}
		foreach(@key: @value in @pvp) {
			msg(@key.': '.color('gray').@value)
		}

	case 'spectate':
		if(pworld() != 'custom', 
			die('You are not in Frog Park.'))
		@id = to_lower($id)
		@pvp = import('pvp'.@id)
		if(!@pvp || !@pvp['running']) {
			die('Game is not running.')
		}
		if(array_index_exists(@pvp['players'], player())) {
			die('You\'re already in the game.')
		}
		if(array_contains(get_scoreboards(), @id)) {
			set_pscoreboard(player(), @id)
			@pvp = import('pvp'.@id)
			if(array_index_exists(@pvp['arena'], 'resourcepack')) {
				send_resourcepack(player(), 'http://mc.finalscoremc.com:25966/resourcepacks/'.@pvp['arena']['resourcepack'].'.zip')
			}
			_set_spectator(@id, player(), @pvp)
		} else {
			msg('The arena isn\'t running.')
		}
		
	case 'addtime':
		if(!get_command_block() && !has_permission('group.moderators'), die())
		if(!$id, die())
		@id = to_lower($id)
		@pvp = import('pvp'.@id)
		if(!@pvp, die())
		@pvp['arena']['timer'][1] += $team
		_regionmsg(@pvp['arena']['broadcast'], 'Added '.$team.' more minutes.')
		
	case 'removetime':
		if(!has_permission('group.moderators'), die())
		if(!$id, die())
		@id = to_lower($id)
		@pvp = import('pvp'.@id)
		array_remove(@pvp['arena'], 'timer')
		# foreach(@p in @pvp['players']) {
			# if(function_exists('premove_bar')) {
				# premove_bar(@p)
			# }
		# }
		
	case 'start':
		if(!$id, die('You must specify an arena to start. Usage: /pvp start arenaname'))
		@id = to_lower($id)
		@pvp = import('pvp'.@id)
		if(!@pvp, die('There is no match to start.'))
		if(@pvp['running'], die('Match already in progress.'))
		
		@pvp['arena'] = get_value('arena.'.@id)
		if(!@pvp['arena'], die('Can\'t find that arena.'))

		if(array_index_exists(@pvp['arena'], 'parent')) {
			@parent = get_value('arena.'.@pvp['arena']['parent'])
			if(!@parent, die('Can\'t find parent arena.'))
			@primaryName = to_upper(@pvp['arena']['parent']);
			@secondaryName = to_upper(@id)
			@pvp['arena'] = array_merge(@parent, @pvp['arena'])
		} else if(array_index_exists(@pvp['arena'], 'arenaselect')) {
			@childid = array_rand(@pvp['arena']['arenaselect']['arenas'], 1, false)[0]
			@child = get_value('arena', @childid)
			if(!@child, die('Can\'t find child arena.'))
			@primaryName = to_upper(@id);
			@secondaryName = to_upper(@childid)
			@pvp['arena'] = array_merge(@pvp['arena'], @child)	
		} else {
			@primaryName = to_upper(@id);
			@secondaryName = null;
		}
		
		if(array_index_exists(@pvp['arena'], 'sharedarenas')) {
			foreach(@arenaid in @pvp['arena']['sharedarenas']) {
				@otherpvp = import('pvp'.@arenaid)
				if(@otherpvp && @otherpvp['running']) {
					die('Another PVP match is running in that region.')
				}
			}
		}
		
		if(player() == '~console' || pworld() != 'custom', die('You can only run this in Frog Park.'))

		# CHECK ARENA SETTINGS
		if(!array_index_exists(@pvp['arena'], 'lobby'), die('No lobby defined for arena.'))
		if(!array_index_exists(@pvp['arena'], 'region'), die('No region defined for arena.'))
		if(!array_index_exists(@pvp['arena'], 'spawn'), die('No spawns defined for arena.'))
		if(!array_index_exists(@pvp['arena'], 'min'), @pvp['arena']['min'] = 2)
		if(!array_index_exists(@pvp['arena'], 'broadcast'), @pvp['arena']['broadcast'] = @pvp['arena']['region'])
		if(!array_index_exists(@pvp['arena'], 'mode'), @pvp['arena']['mode'] = 'dm')
		if(!array_index_exists(@pvp['arena'], 'ff'), @pvp['arena']['ff'] = true)
		if(!array_index_exists(@pvp['arena'], 'flags'), @pvp['arena']['flags'] = array())
		if(!array_index_exists(@pvp['arena'], 'stats'), @pvp['arena']['stats'] = true)
		if(!array_index_exists(@pvp['arena'], 'delay')) {
			if(array_index_exists(@pvp['arena'], 'classes')) {
				@pvp['arena']['delay'] = 45
			} else {
				@pvp['arena']['delay'] = 10
			}
		}
		if(array_contains(@pvp['arena']['flags'], 'keepinventory'), @pvp['arena']['denydrop'] = 'all')
		
		# dynamically determine if it should be a team game
		if(@pvp['arena']['mode'] == 'ddm') {
			@size = array_size(@pvp['players'])
			if(@size > 5 && @size % 2 == 0) {
				@pvp['arena']['mode'] = 'tdm'
				@pvp['arena']['ff'] = false
				if(array_index_exists(@pvp['arena'], 'kit')) {
					@pvp['arena']['kit'][1] = @pvp['arena']['kit'][0]
				}
				
				@spawns = @pvp['arena']['spawn'][0][]
				@pvp['arena']['spawn'] = array()
				@team = 0
				foreach(@spawn in @spawns) {
					@pvp['arena']['spawn'][@team] = @spawn
				}
				
			} else {
				@pvp['arena']['mode'] = 'dm'
			}
		}
		
		if(!array_index_exists(@pvp['arena'], 'team') && array_contains(array('ctf', 'tdm', 'infection'), @pvp['arena']['mode'])) {
			@pvp['arena']['team'] = array(
				array('name': 'Red', 'color': color('c')), 
				array('name': 'Blue', 'color': color('3'))
			)
		} else if(array_index_exists(@pvp['arena'], 'team')) {
			@pvp['arena']['team'][0]['color'] = color(@pvp['arena']['team'][0]['color'])
			@pvp['arena']['team'][1]['color'] = color(@pvp['arena']['team'][1]['color'])
		}
		if(!array_index_exists(@pvp['arena'], 'score')) {
			switch(@pvp['arena']['mode']) {
				case 'ctf':
					@pvp['arena']['score'] = 5
				case 'koth':
					@pvp['arena']['score'] = 60
			}
		}
		
		if(array_contains(@pvp['arena']['flags'], 'debug') && !array_contains(pgroup(), 'builders'), die('This arena is still in testing and can only be started by Builders.'))
		
		foreach(array_keys(@pvp['players']), @player) {
			if(!ponline(@player) || pworld(@player) != 'custom') {
				array_remove(@pvp['players'], @player)
			}
		}
		if(array_size(@pvp['players']) < @pvp['arena']['min'], die('There aren\'t enough players. ('.array_size(@pvp['players']).')'))
		
		if(array_contains(get_scoreboards(), @id), remove_scoreboard(@id))
		create_scoreboard(@id)
		
		# PREPARE TEAMS
		if(array_index_exists(@pvp['arena'], 'team')) {
			if(@pvp['arena']['mode'] == 'ctf') {
				@pvp['team'][0]['score'] = 0
				@pvp['team'][0]['flag'] = 0
				@pvp['team'][1]['score'] = 0
				@pvp['team'][1]['flag'] = 0
			}
			if(array_index_exists(@pvp['arena'], 'captain')) {
				@pvp['team'][0]['hatentity'] = 0
				@pvp['team'][1]['hatentity'] = 0
				@pvp['team'][0]['captain'] = ''
				@pvp['team'][1]['captain'] = ''
			}
			create_team(@pvp['arena']['team'][0]['name'], @id)
			create_team(@pvp['arena']['team'][1]['name'], @id)
			set_team_display(@pvp['arena']['team'][0]['name'], array('prefix': @pvp['arena']['team'][0]['color']), @id)
			set_team_display(@pvp['arena']['team'][1]['name'], array('prefix': @pvp['arena']['team'][1]['color']), @id)
			set_team_options(@pvp['arena']['team'][0]['name'], array('friendlyinvisibles': true), @id)
			set_team_options(@pvp['arena']['team'][1]['name'], array('friendlyinvisibles': true), @id)
		}

		switch(@pvp['arena']['mode']) {
		case 'koth':
			create_objective('seconds', 'DUMMY', @id)
			set_objective_display('seconds', array('displayname': color('a').'Seconds Left', 'slot': 'SIDEBAR'), @id)
		case 'tdm':
		case 'dm':
			if(array_index_exists(@pvp['arena'], 'lives') && @pvp['arena']['lives'] > 1) {
				create_objective('lives', 'DUMMY', @id)
				set_objective_display('lives', array('displayname': color('a').'Lives Left', 'slot': 'SIDEBAR'), @id)
			}
			if(array_index_exists(@pvp['arena'], 'mobprotect')) {
				create_objective('mobhealth', 'DUMMY', @id)
				set_objective_display('mobhealth', array('displayname': color('a').'Mob Health', 'slot': 'SIDEBAR'), @id)
				team_add_player(@pvp['arena']['team'][0]['name'], @pvp['arena']['team'][0]['name'], @id)
				team_add_player(@pvp['arena']['team'][1]['name'], @pvp['arena']['team'][1]['name'], @id)
			}
		case 'ctf':
			create_objective('captures', 'DUMMY', @id)
			set_objective_display('captures', array('displayname': color('a').'Captures ('.@pvp['arena']['score'].')', 'slot': 'SIDEBAR'), @id)
			team_add_player(@pvp['arena']['team'][0]['name'], @pvp['arena']['team'][0]['name'], @id)
			set_pscore('captures', @pvp['arena']['team'][0]['name'], 0, @id)
			team_add_player(@pvp['arena']['team'][1]['name'], @pvp['arena']['team'][1]['name'], @id)
			set_pscore('captures', @pvp['arena']['team'][1]['name'], 0, @id)
		}
		
		if(array_index_exists(@pvp[arena], 'team')) {
			if(!array_index_exists(@pvp, 'team')) {
				@pvp[team] = array(array(), array())
			}
			if(!array_index_exists(@pvp[team][0], 'players')) {
				@pvp[team][0][players] = array()
				@pvp[team][1][players] = array()
			}
			if(!array_index_exists(@pvp[arena], 'teamratio')) {
				@pvp[arena][teamratio] = array(1, 1)
			}
			@players = array_keys(@pvp[players])
			for(@i = 0, @i < array_size(@pvp[players]), @i++) {
				@index = rand(0, array_size(@players))
				@player = @players[@index]
				_assign_team(@player, @pvp)
				array_remove(@players, @index)
			}
		}
		
		if(array_index_exists(@pvp['arena'], 'captain')) {
			@pvp['team'][0]['captainhat'] = @pvp['arena']['classes'][@pvp['arena']['captain'][0]]['kit'][103]
			@pvp['team'][1]['captainhat'] = @pvp['arena']['classes'][@pvp['arena']['captain'][1]]['kit'][103]
		}

		foreach(@p in array_keys(@pvp['players'])) {
			if(pmode(@p) == 'CREATIVE', set_pmode(@p, 'SURVIVAL'))
			if(array_index_exists(@pvp['arena'], 'resourcepack')) {
				send_resourcepack(@p, 'http://mc.finalscoremc.com:25966/resourcepacks/'.@pvp['arena']['resourcepack'].'.zip')
			}
		}

		@pvp['coins'] = if(array_size(@pvp['players']) > 2, 1, 0)
		@pvp['stats'] = associative_array()
		@pvp['binds'] = array()
		@pvp['spectators'] = array()
		@pvp['running'] = 1
		export('currently', @primaryName)
		
		# WEAPONS
		
		if(array_index_exists(@pvp['arena'], 'weapons')) {
			foreach(@weapon in @pvp['arena']['weapons']) {
				include('weapons.library/'.@weapon.'.ms')
			}
		}
		
		# CLASSES
		if(array_index_exists(@pvp['arena'], 'classes')) {
			_class_select(@id, @pvp)
		
			#ANNOUNCE
			_worldmsg('custom', color('gray').'[PVP] '.color('r')
				.'Starting '.@primaryName.if(@secondaryName, ': '.@secondaryName).' soon...');
		
		} else {
			_worldmsg('custom', color('gray').'[PVP] '.color('r')
				.'Starting '.@primaryName.if(@secondaryName, ': '.@secondaryName)
				.' in '.@pvp['arena']['delay'].' seconds...');
		
		}

		if(array_index_exists(@pvp, 'team')) {
			_worldmsg('custom', @pvp['arena']['team'][0]['color'].'['.@pvp['arena']['team'][0]['name'].'] '
				.array_implode(@pvp['team'][0]['players'], ' ').'\n'.color('r')
				.@pvp['arena']['team'][1]['color'].'['.@pvp['arena']['team'][1]['name'].'] '
				.array_implode(@pvp['team'][1]['players'], ' '))
		} else {
			_worldmsg('custom', array_implode(array_keys(@pvp['players']), ' vs '))
		}
		
		play_sound(@pvp['arena']['lobby'], array('sound': 'WITHER_SPAWN', 'pitch': 1.5, 'volume': 3))
		
		#START ROUND
		@timeleft = array(@pvp['arena']['delay']);
		set_interval(1000, closure(){
			@timeleft[0] -= 1;
			if(@timeleft[0] > 0) {
				if(@timeleft[0] <= 3) {
					play_sound(@pvp['arena']['lobby'], array('sound': 'NOTE_PLING', 'pitch': 1, 'volume': 3));
					if(extension_exists('CHNaughty')) {
						foreach(@p in array_keys(@pvp['players'])) {
							title_msg(@p, color('yellow').@timeleft[0], null, 0, @timeleft[0] * 20, 20);
						}
					}
				} else if(@timeleft[0] > 4 && array_index_exists(@pvp['arena'], 'classes')) {
					@choosing = false;
					foreach(@p in array_keys(@pvp['players'])) {
						if(ponline(@p) && !is_null(pget_virtualchest(@p))) {
							@choosing = true;
							break();
						}
					}
					if(!@choosing) {
						@timeleft[0] = 4;
					}
				}
				if(@timeleft[0] == 4) {
					if(extension_exists('CHNaughty')) {
						foreach(@p in array_keys(@pvp['players'])) {
							title_msg(@p, null, @primaryName.if(@secondaryName, ': '.@secondaryName), 20, 60, 20);
						}
					}
				}
			} else {
				if(extension_exists('CHNaughty')) {
					foreach(@p in array_keys(@pvp['players'])) {
						title_msg(@p, color('green').'GO!', null, 0, 0, 20);
					}
				}
				clear_task();
				@pvp['running'] = 2;
				_pvp_start_match(@id);
			}
		});
		
	case 'end':
		if(!get_command_block() && !has_permission('group.builders'), die(color('red').'You do not have permission.'))
		if(!$id, die(color('gold').'Usage: /pvp end arenaName winningTeam#'))
		@id = to_lower($id)
		@pvp = import('pvp'.@id)
		if(!@pvp || @pvp['running'] < 2, die(color('red').'Not running.'))
		if(is_numeric($team)) {
			_pvp_end_match(@id, @pvp['team'][$team - 1]['players'])
		} else {
			_pvp_end_match(@id, array())
		}
		
	default:
		msg('/pvp join <id> '.color('gray').'Join the match')
		msg('/pvp start <id> '.color('gray').'Start the match')
		msg('/pvp spectate <id> '.color('gray').'Displays the scoreboard')
		msg('/pvp end <id> <team> '.color('gray').'End in favor of team')
		msg('/pvp addtime <id> <minutes> '.color('gray').'Add time to the timer')
		msg('/pvp removetime <id> <minutes> '.color('gray').'Remove time from the timer')
		msg('/pvp debug <id> '.color('gray').'Output debug information')
	}
<<<	