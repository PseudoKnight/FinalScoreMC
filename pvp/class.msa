*:/class [$cmd] [$id] [$setting] [$] = >>>
if(!has_permission('group.builders'), die('You do not have permission.'))
if($, @args = parse_args($), @args = array())
array_resize(@args, 3)
switch($cmd) {
case 'set':
	try(@arenaid = split(':', $id)[0]
	@classid = split(':', $id)[1], die('You need to specify an arena.'))
	@arena = get_value('arena', @arenaid)
	if(!@arena, die('No arena by that name: '.@arenaid))
	if(!array_index_exists(@arena['classes'], @classid)) {
		@arena['classes'][@classid] = array()
	}
	switch($setting) {
	case 'selector':
		if(!pinv(player(), null), die('You must select an item on your hotbar.'))
		@arena['classes'][@classid]['selector'] = pinv(player(), null)
		msg('Class selector set to this item.')
	
	case 'kit':
		@inv = pinv()
		foreach(array_keys(@inv), @slot) {
			if(@inv[@slot] == null) {
				array_remove(@inv, @slot)
			}
		}
		@arena['classes'][@classid]['kit'] = @inv
		msg('Set kit to current inventory.')
		
	case 'speed':
		if(@args[0] < 0 || @args[0] > 1) {
			die('Must be betwee 0 and 1')
		}
		@arena['classes'][@classid]['speed'] = @args[0]
		msg('Set class speed to '.@args[0].' (default 0.2)')
		
	case 'hunger':
		if(@args[0] < 0 || @args[0] > 20) {
			die('Hunger must be within 0 and 20')
		}
		if(!@args[1]) {
			@args[1] = 5
		} else if(@args[1] && (@args[1] < 0 || @args[1] > 20)) {
			die('Saturation must be within 0 and 20')
		}
		@arena['classes'][@classid]['hunger'] = array(@args[0], @args[1])
		msg('Set hunger to '.@args[0].' hunger and '.@args[1].' saturation.')

	case 'effect':
		@effects = _get_effects()
		if(!array_contains(array_keys(@effects), @args[0]), die('Unknown potion effect. '.array_keys(@effects)))
		@effect = @effects[@args[0]]
		if(!array_index_exists(@arena['classes'][@classid], 'effect'), @arena['classes'][@classid]['effect'] = associative_array())
		if(@args[1] && @args[2]) {
			if(@args[1] == 0 || @args[2] == 0) {
				array_remove(@arena['classes'][@classid]['effect'], @effect)
				if(array_size(@arena['classes'][@classid]['effect']) == 0) {
					array_remove(@arena['classes'][@classid], 'effect')
				}
				msg('Removed potion effect '.@args[0].'.')
			} else {
				@arena['classes'][@classid]['effect'][@effect] = associative_array('strength': @args[1] - 1, 'length': @args[2])
				msg('Set a potion effect for this class: '.@args[0].' with a strength of '.@args[1].' and a length of '.@args[2].' seconds.')
			}
		} else {
			die('Requires a strength integer and a length integer in seconds.')
		}
	
	case 'spawner':
		if($ == 'true') {
			@arena['classes'][@classid]['spawner'] = true
			msg('Turned on spawning for class.')
		} else if($ == 'false' && array_index_exists(@arena['classes'][@classid], 'spawner')) {
			array_remove(@arena['classes'][@classid], 'spawner')
			msg('Turned off spawning for class.')
		}
		
	case 'doublejump':
		if($ == 'true') {
			@arena['classes'][@classid]['doublejump'] = 'normal' # string instead of boolean leaves room for alternative doublejump modes
			msg('Turned on doublejump for class.')
		} else if($ == 'false' && array_index_exists(@arena['classes'][@classid], 'doublejump')) {
			array_remove(@arena['classes'][@classid], 'doublejump')
			msg('Turned off doublejump for class.')
		}
	
	case 'script':
		if($ == 'true') {
			@arena['classes'][@classid]['script'] = true
			msg('Turned ON special script loading for this class.')
		} else if($ == 'false' && array_index_exists(@arena['classes'][@classid], 'script')) {
			array_remove(@arena['classes'][@classid], 'script')
			msg('Turned OFF special script loading for this class.')
		}
	
	case 'xp':
		if(is_numeric($) && $ <= 100 && $ >= 0) {
			@arena['classes'][@classid]['xp'] = $
			msg('Set class to start with '.$.'% of their experience bar.')
		} else {
			die('It needs to be a number from 0 to 100')
		}
		
	default:
		die('Available settings: selector, kit, effect, hunger, speed, spawner, doublejump, script')
	}
	store_value('arena', @arenaid, @arena)
	
case 'delete':
	try(@arenaid = split(':', $id)[0]
	@classid = split(':', $id)[1], die('You need to specify an arena.'))
	@arena = get_value('arena', @arenaid)
	if(!@arena, die('No arena by that name: '.@arenaid))
	if(!array_index_exists(@arena['classes'], @classid), die('No class by that name.'))
	if($setting) {
		array_remove(@arena['classes'][@classid], $setting)
		msg($setting.' deleted from '.@classid)
	} else {
		array_remove(@arena['classes'], @classid)
		msg('Class deleted.')
	}
	store_value('arena', @arenaid, @arena)

case 'info':
	try(@arenaid = split(':', $id)[0]
	@classid = split(':', $id)[1], die('You need to specify an arena.'))
	@arena = get_value('arena', @arenaid)
	if(!@arena, die('No arena by that name: '.@arenaid))
	if(!array_index_exists(@arena['classes'], @classid), die('No class by that name.'))
	foreach(@arena['classes'][@classid], @setting, @value) {
		msg(color('gray').@setting.' '.color('r').@value)
	}
	
case 'rename':
	try(@arenaid = split(':', $id)[0]
	@classid = split(':', $id)[1], die('You need to specify an arena.'))
	@arena = get_value('arena', @arenaid)
	if(!@arena, die('No arena by that name: '.@arenaid))
	if(!array_index_exists(@arena['classes'], @classid), die('No class by that name.'))
	@arena['classes'][$setting] = @arena['classes'][@classid]
	array_remove(@arena['classes'], @classid)
	store_value('arena', @arenaid, @arena)
	msg('Changed '.@classid.' class name to '.to_lower($setting))
	
# case 'load':
	# include('includes.library/class.ms')
	# _class('load', player(), $id)
	
# case 'unload':
	# include('includes.library/class.ms')
	# _class('unload', player(), $id)
	
case 'equip':
	try(@arenaid = split(':', $id)[0]
	@classid = split(':', $id)[1], die('You need to specify an arena.'))
	@arena = get_value('arena', @arenaid)
	if(!@arena, die('No arena by that name: '.@arenaid))
	@pvp = array()
	@pvp['arena'] = @arena
	include('includes.library/class.ms')
	_class('equip', player(), @classid, @pvp)
	
case 'selector':
	try(@arenaid = split(':', $id)[0]
	@classid = split(':', $id)[1], die('You need to specify an arena.'))
	@arena = get_value('arena', @arenaid)
	if(!@arena, die('No arena by that name: '.@arenaid))
	set_pinv(player(), array(null: @arena['classes'][@classid]['selector']))
	
case 'list':
	@arena = get_value('arena', $id)
	if(!@arena, die('No arena by that name: '.$id))
	@list = ''
	foreach(@class in array_keys(@arena['classes'])) {
		@list .= @class.' '
	}
	msg(color('gray').'CLASSES IN '.to_upper($id).': '.color('r').@list)
	
default:
	die('Usage: /class [set|delete|info|load|unload|equip|list] [classname] [setting] [value[s]]')
}
<<<