# Sending chat from the console
/s $name $ = >>>
	broadcast(color('8').simple_date('h:mm').color('gray').' [console] '.color('aqua').$name.color('dark_gray').': '.color('r').$) 
	if(function_exists('dm_broadcast_to_web')) {
		dm_broadcast_to_web($, $name)
	}
<<<

*:/say $ = >>>
	@ignorelist = import('ignorelist')
	if(array_index_exists(@ignorelist, player()) && array_contains(@ignorelist[player()], 'all')) {
		msg('You have been muted.')
		die()
	}
	broadcast(color('8').simple_date('h:mm').' '.if(player() != '~console' && array_contains(pgroup(), 'donor'), color('green').'\u2666 ')._colorname().player().color('gray').': '.color('r').$) 
<<<

*:/me $ = >>>
	@ignorelist = import('ignorelist')
	if(array_index_exists(@ignorelist, player()) && array_contains(@ignorelist[player()], 'all')) {
		msg('You have been muted.')
		die()
	}
	broadcast('* '.player().' '.colorize($))
	if(function_exists('dm_broadcast_to_web')) {
		dm_broadcast_to_web('* '.player().' '.$)
	}
<<<

# MUTING
*:/ignore $player = >>>
	if($player == 'all') {
		@player = 'all'
	} else {
		try(@player = player($player), die('No player found.'))
	}
	@ignorelist = import('ignorelist')
	if(!array_index_exists(@ignorelist, @player)) {
		@ignorelist[@player] = array()
	}
	if(array_contains_ic(@ignorelist[@player], player()), die('You are already ignoring '.@player.'.'))
	@ignorelist[@player][] = player()
	msg('You are now ignoring '.@player.'.')
	export('ignorelist', @ignorelist)
<<<

*:/unignore $player = >>>
	if($player == 'all') {
		@player = 'all'
	} else {
		try(@player = player($player), die('No player found.'))
	}
	@ignorelist = import('ignorelist')
	if(!array_index_exists(@ignorelist, @player)) {
		die('Wasn\'t ignored.')
	} else if(!array_contains_ic(@ignorelist[@player], player())) {
		die('You have not ignored that player.')
	}
	for(@i = 0, @i < array_size(@ignorelist[@player]), @i++) {
		if(@ignorelist[@player][@i] == player()) {
			array_remove(@ignorelist[@player], @i)
			msg('You are no longer ignoring '.@player.'.')
			break()
		}
	}
	export('ignorelist', @ignorelist)
<<<

*:/mute $player = >>>
	if(!has_permission('group.moderator'), die('You do not have permission to use this command.'))
	try(@player = player($player), die('No player found.'))
	@ignorelist = import('ignorelist')
	if(!array_index_exists(@ignorelist, @player)) {
		@ignorelist[@player] = array()
	}
	if(array_contains(@ignorelist[@player], 'all')) {
		die('Already muted.')
	}
	@ignorelist[@player][] = 'all'
	msg(@player.' is now muted.')
	export('ignorelist', @ignorelist)
<<<

*:/unmute $player = >>>
	if(!has_permission('group.moderator'), die('You do not have permission to use this command.'))
	try(@player = player($player), die('No player found.'))
	@ignorelist = import('ignorelist')
	if(!array_index_exists(@ignorelist, @player) || !array_contains(@ignorelist[@player], 'all')) {
		die('That player isn\'t muted.')
	}
	for(@i = 0, @i < array_size(@ignorelist[@player]), @i++) {
		if(@ignorelist[@player][@i] == 'all') {
			array_remove(@ignorelist[@player], @i)
			msg(@player.' is no longer muted.')
			break()
		}
	}
	export('ignorelist', @ignorelist)
<<<

# MESSAGING #
*:/msg $player $ = >>>
	include('includes.library/chat.ms')
	_pmsg($player, $)
<<<
*:/tell $player $ = >>>
	include('includes.library/chat.ms')
	_pmsg($player, $)
<<<
*:/w $player $ = >>>
	include('includes.library/chat.ms')
	_pmsg($player, $)
<<<

*:/unsend = >>>
	@player = import(player().'_mail');
	if(is_null(@player)) {
		die('There are no offline messages for you to unsend. Perhaps you waited too long.');
	}
	@pdata = _pdata(@player);
	if(!array_index_exists(@pdata, 'mail')) {
		die('Cannot find last sent mail to '.@player.'. Perhaps they read it already.');
	}
	@letter = null;
	@id = -1;
	foreach(@i: @l in @pdata['mail']) {
		if(@l[1] == player()) {
			@letter = @l;
			@id = @i;
		}
	}
	if(is_null(@letter)) {
		die('Cannot find last sent mail to '.@player.'.');
	}
	array_remove(@pdata['mail'], @i);
	if(array_size(@pdata['mail']) == 0) {
		array_remove(@pdata, 'mail');
	}
	_store_pdata(@player, @pdata);
	msg('Removed last sent offline message to '.@player.'.');
<<<

*:/r $ = >>>
	@conv = import('conv')
	try(@player = @conv[player()], die('You do not have any current conversations.'))
	if(@player != '~console' && !ponline(@player), die('That player is no longer online.'))
	include('includes.library/chat.ms')
	_pmsg(@player, $)
<<<

# Staff Chat Channel Toggle
*:/staffchat = >>>
	if(!has_permission('chat.staff'), die('You do not have permission.'))
	@staffchat = import('staffchat')
	if(is_null(@staffchat)) {
		@staffchat = array()
	}
	
	if(array_contains(@staffchat, player())) {
		array_remove_values(@staffchat, player())
		export('staffchat', @staffchat)
		msg('You are no longer in staff chat.')
	} else {
		@staffchat[] = player()
		export('staffchat', @staffchat)
		msg('You are now in staff chat.')
	}
<<<