*:/home [$] = >>>
	if(array_contains(array('outworld', 'outworld_nether', 'outworld_the_end'), pworld())) {
		die('You cannot teleport in this world.')
	}
	if(!$) {
		@world = pworld()
		@player = player()
	} else if(array_contains(get_worlds(), _worldid(to_lower($)))) {
		@world = _worldid(to_lower($))
		@player = player()
	} else {
		@world = pworld()
		@player = $
	}
	@pdata = get_value('player.'.to_lower(@player))
	if(!@pdata, die('Cannot find a home.'))
	if(!array_index_exists(@pdata, 'homes') || !array_index_exists(@pdata['homes'], @world), die('You can set a home for a world with /sethome'))
	set_peffect(player(), 9, 1, 7)
	@loc = ploc()
	play_sound(@loc, array('sound': 'PORTAL_TRIGGER', 'volume': 1, 'pitch': 1))
	set_timeout(3700, closure(){
		if(!ponline(player()), die())
		play_sound(@loc, array('sound': 'GHAST_FIREBALL', 'volume': 2, 'pitch': 1))
		make_effect(@loc, 'MOBSPAWNER_FLAMES')
		make_effect(_relative(@loc, 'up'), 'MOBSPAWNER_FLAMES')
		set_ploc(player(), array(
			@pdata['homes'][@world][0], 
			@pdata['homes'][@world][1],
			@pdata['homes'][@world][2],
			@world,
		))
		pfacing(@pdata['homes'][@world][3], @pdata['homes'][@world][4])
		set_peffect(player(), 15, 1, 1)
		play_sound(@loc, array('sound': 'GHAST_FIREBALL', 'volume': 2, 'pitch': 1))
		make_effect(@loc, 'MOBSPAWNER_FLAMES')
		make_effect(_relative(@loc, 'up'), 'MOBSPAWNER_FLAMES')
	})
<<<

*:/sethome [$] = >>>
	if(array_contains(array('outworld', 'outworld_nether', 'outworld_the_end', 'custom'), pworld())) {
		die('You cannot set homes here.')
	}
	@pdata = get_value('player.'.to_lower(player()))
	if(!array_index_exists(@pdata, 'homes'), @pdata['homes'] = array())
	@loc = ploc()
	@facing = pfacing()
	array_set(@pdata['homes'], pworld(), array(
		floor(@loc[0]) + 0.5,
		@loc[1],
		floor(@loc[2]) + 0.5,
		round(@facing[0], 2),
		round(@facing[1], 2)
	))
	store_value('player.'.to_lower(player()), @pdata)
	msg('Set your home to this location.')
<<<

*:/warp [$] = >>>
	if(!$) {
		call_alias('/warps list')
		die()
	}
	
	@args = parse_args($)
	if(array_size(@args) == 2) {
		if(get_command_block()) {
			@player = @args[0]
			@warpid = @args[1]
		} else {
			die('You cannot teleport others.')
		}
	} else {
		 @player = player()
		 @warpid = @args[0]
	}

	@warp = get_value('warp.'.to_lower(@warpid))
	if(!@warp, die('That warp does not exist.'))
	if(array_contains(array('outworld', 'outworld_nether', 'outworld_the_end'), pworld(@player)), die('You cannot warp in this world.'))
	include('includes.library/teleports.ms')
	_warmuptp(3.7, @player, @warp['loc'], @warp['yaw'], @warp['pitch'])
<<<

*:/setwarp [$warp] = >>>
	if(!has_permission('group.moderators'), die('You do not have permission to use this command.'))
	if(!$warp, die('You need to specify a warp name.'))
	if(reg_count('\\w', $warp) < 0, die('This needs to be in alphanumeric characters.'))
	@loc = ploc()
	@loc = array(round(@loc[0], 1), @loc[1], round(@loc[2], 1), @loc[3])
	if(function_exists('dm_delete_marker')) {
		if(has_value('warp.'.$warp)) {
			dm_delete_marker('warps', 'warp.'.$warp)
		}
	}
	store_value('warp.'.$warp, array('loc': @loc, 'yaw': round(pfacing()[0]), 'pitch': round(pfacing()[1])))
	if(function_exists('dm_create_marker')) {
		dm_create_marker('warps', array('id': 'warp.'.$warp, 'label': 'WARP.'.to_upper($warp), 'location': @loc, 'world': @loc[3], 'icon': 'star', 'persistent': true)) # radius is workaround for a bug
	}
	msg('Warp created.')
<<<

*:/warps [$cmd] [$id] = >>>
	switch($cmd) {
	case 'del':
	case 'delete':
		if(!has_permission('group.moderators'), die('You do not have permission to use this command.'))
		if(!$id, die('This requires a warp name.'))
		if(!has_value('warp.'.$id), die('There is no warp by that name.'))
		clear_value('warp.'.$id)
		if(function_exists('dm_delete_marker')) {
			dm_delete_marker('warps', 'warp.'.$id)
		}
		msg('Deleted warp.')
	case 'list':
		@warps = get_values('warp')
		@main = ''
		@park = ''
		@dev = ''
		foreach(@warp in array_keys(@warps)) {
			if(@warps[@warp]['loc']['3'] == 'custom') {
				@park .= split('.', @warp)[1].' '
			} else if(@warps[@warp]['loc']['3'] == 'dev') {
				@dev .= split('.', @warp)[1].' '
			} else {
				@main .= split('.', @warp)[1].' '
			}
		}
		msg('AVAILABLE WARPS:\n'
		.color('green').'[MAIN] '.color('r').@main.'\n'
		.color('aqua').'[FROG PARK] '.color('r').@park.'\n'
		.color('red').'[DEV] '.color('r').@dev)
	case 'resetmarkers':
		if(!has_permission('group.administrators'), die('You do not have permission to use this command.'))
		if(!function_exists('dm_all_markersets')) { die('Uneditable at this time.') }
		if(array_contains(if(function_exists('dm_all_markersets')) { dm_all_markersets() }, 'warps')) {
			if(function_exists('dm_delete_markerset')) {
				dm_delete_markerset('warps')
			}
		}
		
		if(function_exists('dm_create_markerset')) {
			dm_create_markerset('warps', array('label': 'Warps', 'persistent': true))
		}
		if(function_exists('dm_set_markerset_hide_by_default')) {
			dm_set_markerset_hide_by_default('warps', true)
		}
		
		@warps = get_values('warp')
		
		foreach(@name: @warp in @warps) {
			if(function_exists('dm_create_marker')) {
				dm_create_marker('warps', array('id': @name, 'label': to_upper(@name), 'location': @warp['loc'], 'world': @warp['loc'][3], 'icon': 'star', 'persistent': true))
			}
		}

		msg(color('green').'Done.')
		
	default:
		msg('Available sub-commands: "/warps del <warp>", "/warps list".')
	}
<<<

*:/spawn [$world] = >>>
	@blockedworlds = array('outworld', 'outworld_nether', 'outworld_the_end')
	if(array_contains(@blockedworlds, $world) || array_contains(@blockedworlds, pworld())) {
		msg('You cannot teleport directly to or from that world.')
	} else if($world) {
		@world = _worldid($world)
		if(!array_contains_ic(get_worlds(), @world), die('Failed to find a world called '.$world.'.'))
		include('includes.library/teleports.ms')
		@loc = get_spawn(@world)
		@loc = array(@loc[0] + 0.5, @loc[1] - 1, @loc[2] + 0.5, @loc[3])
		_warmuptp(3.7, player(), @loc)
	} else {
		include('includes.library/teleports.ms')
		@loc = get_spawn()
		@loc = array(@loc[0] + 0.5, @loc[1] - 1, @loc[2] + 0.5, @loc[3])
		_warmuptp(3.7, player(), @loc)
	}
<<<

# TELEPORTATION BETWEEN FRIENDS #
*:/join $player = >>>
	try(@player = player($player), die('Player not found.'))
	@blockedworlds = array('outworld', 'outworld_nether', 'outworld_the_end')
	if(array_contains(@blockedworlds, pworld()), die('You cannot teleport in this galaxy'))
	if(array_contains(@blockedworlds, pworld($player)), die('That player is out of reach.'))
	@requests = import('requests')
	@requests[@player] = array('join', player())
	export('requests', @requests)
	tmsg(@player, color('b').player().' has requested to join you. /accept')
	msg(color('b').'Request to join sent to '.@player.'.')
<<<

*:/invite $player = >>>
	try(@player = player($player), die('Player not found.'))
	@blockedworlds = array('outworld', 'outworld_nether', 'outworld_the_end')
	if(array_contains(@blockedworlds, pworld()), die('You cannot teleport in this galaxy'))
	if(array_contains(@blockedworlds, pworld($player)), die('That player is out of reach.'))
	@requests = import('requests')
	@requests[@player] = array('invite', player())
	export('requests', @requests)
	tmsg(@player, color('b').player().' has requested that you join them. /accept')
	msg(color('b').'Invitation sent to '.@player.'.')
<<<

*:/accept = >>>
	@blockedworlds = array('outworld', 'outworld_nether', 'outworld_the_end')
	if(array_contains(@blockedworlds, pworld()), die('You cannot teleport in this galaxy'))
	@requests = import('requests')
	if(!array_index_exists(@requests, player()), die('There is nothing to accept.'))
	if(!ponline(@requests[player()][1]), die('That player is no longer online.'))
	if(array_contains(@blockedworlds, pworld(@requests[player()][1])), die('That player is out of reach.'))
	include('includes.library/teleports.ms')
	if(@requests[player()][0] == 'invite') {
		msg(color('b').'Teleporting to '.@requests[player()][1].' in 3 seconds...')
		tmsg(@requests[player()][1], color('b').'Teleporting '.player().' in 3 seconds...')
		_warmuptp(3.7, player(), ploc(@requests[player()][1]))
	} else {
		msg(color('b').'Teleporting '.@requests[player()][1].' in 3 seconds...')
		tmsg(@requests[player()][1], color('b').'Teleporting to '.player().' in 3 seconds...')
		_warmuptp(3.7, @requests[player()][1], ploc())
	}
	array_remove(@requests, player())
	export('requests', @requests)
<<<

/tp $player $x $y $z = >>>
	if(get_command_block()) {
		set_ploc($player, $x, $y, $z)
	} else {
		run('/tp' $player $x $y $z)
	}
<<<


*:/goto $player = >>>
	if(!has_permission('group.moderators'), die('You do not have permission.'))
	try(set_ploc(player(), ploc($player)), die('Unknown player.'))
<<<

*:/bring $player = >>>
	if(!has_permission('group.moderators'), die('You do not have permission.'))
	try(set_ploc($player, ploc()), die('Unknown player.'))
<<<

*:/put $player = >>>
	if(!has_permission('group.moderators'), die('You do not have permission.'))
	try(set_ploc($player, pcursor()), die('Unknown player.'))
<<<