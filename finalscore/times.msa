*:/times [$arg='usage'] [$id] [$player] = >>>
	if(!(@id = $id)) {
		@id = sk_current_regions()[-1]
	}
	
	switch($arg) {
	case 'usage':
	
		msg('Usage: /times <top|me|playerName> [courseID]')
		
	case 'top':
	case 'top10':
		
		@times = get_value('times', @id)
		if(!@times || !array_index_exists(@times, 'top')) { 
			die('No top times for '.to_upper(@id).'.')
		}
		msg(color('yellow').color('bold').'=== TOP TIMES - '.to_upper(@id).' ===')
		for(@i = 0, @i < array_size(@times['top']), @i++) {
			msg(color(7).if(@i < 9, ' ').(@i + 1).color('green').' [ '.@times['top'][@i][1].' ] '.color('r').@times['top'][@i][0])
		}
		
	case 'me':
	
		@time = get_value('times.'.@id, player())
		if(!@time) {
			die('You do not have a time for '.to_upper(@id).'.')
		}
		msg('Your best time for '.color('gold').to_upper(@id).color('r').' is '.color('green').@time.' seconds.')
		
	case 'reset':
	
		if(!has_permission('command.resettimes')) {
			die('You do not have permission to reset stats for this course.')
		}
		if(!has_value('times', @id)) {
			die('There are no stats to reset for '.to_upper(@id).'.')
		}
		if($player) {
			if(!has_value('times.'.@id, $player)) {
				die('No player by that name in this course.')
			}
			clear_value('times.'.@id, $player)
			@times = get_value('times', @id)
			foreach(@index: @time in @times['top']) {
				if(@time[0] == $player) {
					array_remove(@times['top'], @index)
					break()
				}
			}
			store_value('times', @id, @times)
			msg('Reset '.$player.' in '.@id)
			
		} else {
			@times = get_values('times', @id)
			foreach(@key: @time in @times) {
				clear_value(@key)
			}
			msg('Reset stats for '.to_upper(@id).'.')
		}
	
	default: 
	
		@time = get_value('times.'.@id, $arg)
		if(!@time) {
			die('No time for '.$arg.' on '.@id.'.')
		}
		msg(color('yellow').$arg.'\'s best time for '.color('gold').to_upper(@id).color('r').' is '.color('green').@time.' seconds.')
		
	}
<<<