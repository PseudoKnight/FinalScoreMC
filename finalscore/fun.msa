*:/acme $player = >>>
	if(get_command_block()) {
		@player = $player
	} else {
		if(!has_permission('group.builders'), die('You do not have permission.'))
		if(!@player = player($player), die('Unknown player.'))
		@message = color('b').'Lovely weather we\'re having today.'
	}
	if(pworld(@player) == 'custom' || pworld(@player) == 'dev') {
		if(get_command_block()) {
			@num = get_value('acme')
			if(!@num) {
				@num = 0
			}
			@num += 1
			@message = color('b').'You are lucky number '.@num.'.'
			store_value('acme', @num)
		}
		@loc = _relative(ploc(@player), 'up', 24)
		if(get_block_at(@loc)[0] != '0', die('Anvil blocked.'))
		set_pmode(@player, 'SURVIVAL')
		set_pinv(@player, array( # guts
			0: array('type': 331, 'data': 0, 'qty': 1),
			1: array('type': 352, 'data': 0, 'qty': 1),
			2: array('type': 376, 'data': 0, 'qty': 1),
			3: array('type': 351, 'data': 15, 'qty': 1),
			4: array('type': 351, 'data': 1, 'qty': 1),
			5: array('type': 363, 'data': 0, 'qty': 1),
			6: array('type': 375, 'data': 0, 'qty': 1),
		))
		set_peffect(@player, 2, 20, 5, true)
		pfacing(@player, pfacing(@player)[0], -90)
		tmsg(@player, @message)
		play_sound(@loc, array('sound': 'CHICKEN_EGG_POP', 'volume': 3))
		set_block_at(@loc, data_values('anvil'))
		set_timeout(5000, closure(
			for(@i = @loc[1], @i > 0, @i--,
				if(split(':', get_block_at(array(@loc[0], @i, @loc[2], @loc[3])))[0] == data_values('anvil')) {
					set_block_at(array(@loc[0], @i, @loc[2], @loc[3]), '0:0')
					make_effect(array(@loc[0], @i, @loc[2], @loc[3]), 'SMOKE:4')
					break()
				}
			)
		))
	}
<<<