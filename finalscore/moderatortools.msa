/safereload [$force] = >>>
	if(!$force) {
	
		@inprogress = array()
		
		@inprogress = array_merge(@inprogress, get_scoreboards())
		array_remove_values(@inprogress, 'main')
		
		@currentspleef = import('currentspleef')
		if(@currentspleef && array_size(@currentspleef) >= 1, array_push(@inprogress, 'Spleef'))

		@cluck = import('cluck')
		if(@cluck && (@cluck['active'] == 1),  array_push(@inprogress, 'Cluck'))
		
		@timers = import('timer')
		if(@timers) {
			foreach(@player: @timer in @timers) {
				array_push(@inprogress, @player.' in '.@timer[0])
			}
		}
		
		if(@inprogress) {
			die('Currently in progress: '.array_implode(@inprogress, ', '))
		}
		
	} else {
	
		@scoreboards = get_scoreboards()
		foreach(@scoreboard in @scoreboards) {
			if(@scoreboard != 'main') {
				remove_scoreboard(@scoreboard)
			}
		}
	
	}
	
	@requests = import('requests')
	if(is_array(@requests), store_value('session.requests', @requests))
	
	@conv = import('conv')
	if(is_array(@conv), store_value('session.conv', @conv))
	
	@conv = import('ignorelist')
	if(is_array(@ignorelist), store_value('session.ignorelist', @ignorelist))
	
	@timers = import('timers')
	if(is_array(@timers), store_value('session.timers', @timers))
	
	run('/save-all')
	set_timeout(2000, closure(){
		msg('Recompiling...')
		run('/reloadaliases')
	})
<<<

/whitelist $player = >>>

	/**
	 * Retrieves a player's mojang UUID from the mojang api, and returns the results
	 * as an array that corresponds to the input array of player names.
	 * @param players An array of players that will be checked.
	 * @param closure The callback closure, which will be called once the
	 * request finishes. It will be sent the array of returned values.
	 * Author: kookster
	 */
	proc _player_uuid(@players, @closure) {
		@success = closure(@response){
			@array = json_decode(@response['body'])
			execute(@array, @closure)
		}
		@options = array(
			'method': 'POST',
			'headers': array('Content-Type': 'application/json'),
			'params': json_encode(@players),
			'success': @success
		)
		http_request('https://api.mojang.com/profiles/minecraft', @options)
	}
	
	_player_uuid(array($player), closure(@array){ 
		try(
			run('/perm player setgroup '.@array[0]['id'].' Everyone')
		, msg('Unknown player.'))
	})
<<<

/hologram $ = >>>
	@loc = _relative(ploc(), 'up', 57)	
	@lines = split(',', $)
	for(@i = array_size(@lines) - 1, @i >= 0, @i--) {
		@skull = spawn_entity('WITHER_SKULL', 1, @loc)[0]
		@horse = spawn_mob('HORSE', 1, @loc)[0]
		set_entity_rider(@skull, @horse)
		set_entity_spec(@skull, array('direction': array(0, 0, 0)))
		set_mob_age(@horse, -1700000, true)
		set_mob_name(@horse, colorize(@lines[@i]))
		set_name_visible(@horse, true)
		@loc['y'] += 0.28
	}
<<<


/butcher [$] = call_alias('/remove '.$)
/remove [$] = >>>
	if(!$) {
		msg('Usage: /remove [radius] [entityType] [-n]')
		msg('This will preserve named mobs, except when given the -n flag.')
		msg('entityType groups: living, hostile')
	}
	@removenamed = false
	@type = array(WITHER, BAT, WITCH, CREEPER, SKELETON, SPIDER, GIANT, ZOMBIE, SLIME, GHAST, PIG_ZOMBIE, ENDERMAN, CAVE_SPIDER, SILVERFISH, BLAZE, MAGMA_CUBE, ENDER_DRAGON, PIG, SHEEP, COW, CHICKEN, SQUID, WOLF, MUSHROOM_COW, SNOWMAN, OCELOT, IRON_GOLEM, VILLAGER, HORSE)
	@radius = 64
	@loc = ploc()
	
	foreach(@arg in parse_args($)) {
		if(is_numeric(@arg)) {
			@radius = min(@arg, 128)
		} else if(@arg == '-n') {
			@removenamed = true
		} else {
			switch(@arg) {
				case 'living':
					continue()
				case 'hostile':
					@type = array(WITHER, WITCH, CREEPER, SKELETON, SPIDER, GIANT, ZOMBIE, SLIME, GHAST, PIG_ZOMBIE, ENDERMAN, CAVE_SPIDER, SILVERFISH, BLAZE, MAGMA_CUBE, ENDER_DRAGON)
				case 'hologram':
					@type = array(WITHER_SKULL, HORSE)
					@removenamed = true
					@loc = _relative(@loc, 'up', 57)
				default:
					@type = @arg
			}
		}
	}
	@entities = entities_in_radius(@loc, @radius, @type)
	@count = 0
	foreach(@e in @entities) {
		if(@removenamed || !is_entity_living(@e) || !get_mob_name(@e)) {
			entity_remove(@e)
			@count++
		}
	}
	msg('Removed '.@count.' of '.array_size(@entities).' in a '.@radius.' radius.')
<<<

/scoreboard remove $scoreboard = >>>
	remove_scoreboard($scoreboard)
<<<

/cleandb = >>>
	@players = get_values('player')
	@count = 0
	foreach(@key: @pdata in @players) {
		if(array_size(@pdata) == 1 && array_index_exists(@pdata, 'ips')) {
			clear_value(@key)
			@count++
		}
	}
	msg('Removed '.@count.' keys.')
<<<

*:/ban [$player] [$] = >>>
	if(!has_permission('group.moderators'), die('You do not have permission.'))
	if(!$player, die('Usage: /ban <player> [-t 1d-8h-10m] [message]'))
	@pdata = get_value('players', to_lower($player))
	if(!@pdata, die('Please use exact names.'))
	@battime = null
	@message = null
	if($) {
		@args = parse_args($)
		if(@args[0] == '-t') {
			@times = split('-', @args[1])
			@bantime = time()
			foreach(@t in @times) {
				switch(substr(@t, length(@t) - 1, length(@t))) {
				case 'd':
					@bantime = @bantime + (86400000 * substr(@t, 0, length(@t) - 1))
				case 'h':
					@bantime = @bantime + (3600000 * substr(@t, 0, length(@t) - 1))
				case 'm':
					@bantime = @bantime + (60000 * substr(@t, 0, length(@t) - 1))
				}
			}
			if(array_size(@args) > 2) {
				@message = array_implode(@args[cslice(2, array_size(@args) - 1)], ' ')
			}
		} else {
			@message = $
		}
	}
	@pdata['ban'] = array()
	if(@bantime, @pdata['ban']['time'] = @bantime)
	if(@message, @pdata['ban']['message'] = @message)
	if(player() != '~console', @pdata['ban']['by'] = player())
	store_value('players', to_lower($player), @pdata)
	if(ponline($player), pkick($player, if(@message, @message, 'Banned. Appeal at FinalScoreMC.com')))
	msg($player.' is now banned.')
	console(player().' banned '.$player.'.'.if(@message, ' (Reason: "'.@message.'")'))
<<<

*:/unban [$player] = >>>
	if(!has_permission('group.moderators'), die('You do not have permission.'))
	if(!$player, die('Usage: /unban <player>'))
	@pdata = get_value('players', to_lower($player))
	if(!@pdata, die('Please use an exact name.'))
	if(!array_index_exists(@pdata, 'ban'), die('That player is not banned.'))
	array_remove(@pdata, 'ban')
	store_value('players', to_lower($player), @pdata)
	msg($player.' is now unbanned.')
	console(player().' unbanned '.$player)
<<<

*:/kick [$player] [$='Kicked.'] = >>>
	if(!$player, die('Usage: /kick <player> <reason>'))
	if(!has_permission('group.moderators'), die('You do not have permission.'))
	try(@player = player($player), die('Unknown player.'))
	pkick(@player, $)
	console(player().' kicked '.@player.'. (Reason: "'.$.'")')
<<<

/homeless $player = >>>
	@pdata = get_value('players', to_lower($player))
	@pdata['homeless'] = true
	store_value('players', to_lower($player), @pdata)
<<<

/clearchat = >>>
	for(@i = 0, @i < 20, @i++) {
		broadcast('')
	}
<<<

*:/contribution $player $amount = >>>
	if(!has_permission('group.administrators'), die('You do not have permission to use this command.'))
	@pdata = get_value('players', to_lower($player))
	if(!@pdata) {
		die('No player by that name.')
	}
	if(array_index_exists(@pdata, 'support')) {
		@pdata['support'] += $amount
		msg('Added $'.$amount.' to '.$player.'\'s contributions; Total: $'.@pdata['support'].'.')
	} else {
		@pdata['support'] = $amount
		msg('Set '.$player.'\'s contribution amount to $'.$amount.'.')
	}
	store_value('players', to_lower($player), @pdata)
	if(!array_contains(pgroup($player), 'donors')) {
		run('/perm player addgroup '.$player.' Donors')
	}
<<<

#Set or reset the MOTD
*:/setmotd [$=''] = >>>
	if(!has_permission('group.moderators'), die('You do not have permission to use this command.'))
	store_value('motd', colorize($))
	msg('MOTD set to: '.colorize($))
<<<

#Run commands as other players and show them which command it was
*:/runas $player $ = >>>
	if(!get_command_block() && !has_permission('group.administrators'), die('You do not have permission for this command'))
	try(@player = player($player), die('No player found.'))
	if(substr($, 0, 2) == '-s') {
		@cmd = substr($, 3)
	} else {
		@cmd = $
		tmsg(@player, color('a').player().' ran this command for you:')
		tmsg(@player, color('6').@cmd)
	}
	console('\''.@cmd.'\' was run on '.@player)
	scriptas(@player,
		if(!call_alias(@cmd)) {
			sudo(@cmd)
		}
	)
<<<

#Alias for setting people to Regulars group
*:/promote $player = >>>
	if(!has_permission('group.administrators'), die('You do not have permission to use this command.'))
	if(array_contains(pgroup($player), 'regulars'), die('Player is already in that group.'))
	run('/perm player setgroup '.$player.' Regulars')
<<<

*:/player $player = >>>
	if(!has_permission('group.moderators'), die('You do not have permission to use this command.'))
	try(
		# ONLINE INFO
		@player = player($player)
		@pdata = get_value('players', to_lower(@player))
		@onlineinfo = pinfo(@player)
		msg(color('gray').'--[ '.color('l').@player.color('gray').' ]-------------------------')
		msg(color('gray').'UUID: '.color('r').@pdata['uuid'])
		msg(color('gray').'Location: '.color('r')._worldname(@onlineinfo[7]).'('.@onlineinfo[7].') '.round(@onlineinfo[1][0]).','.round(@onlineinfo[1][1]).','.round(@onlineinfo[1][2]))
		if(pmode(@player) == 'CREATIVE') {
			msg(color('gray').'Gamemode: '.color('r').'CREATIVE')
		}
		msg(color('gray').'IP: '.color('r').@onlineinfo[3])
		msg(color('gray').'Hostname: '.color('r').@onlineinfo[10])
		@ignorelist = import('ignorelist')
		if(array_index_exists(@ignorelist, @player)) {
			if(array_contains(@ignorelist[@player], 'all')) {
				msg(color('r').'MUTED')
			} else {
				msg(color('gray').'Ignored by: '.color('r').array_implode(@ignorelist[@player], ', '))
			}
		}

	, 
		# OFFLINE INFO
		@pdata = get_value('players', to_lower($player))
		if(!@pdata, die('No stored data for that player name.'))
		@minutes = (time() - plast_played($player)) / 60000
		@hours = @minutes / 60
		@days = @hours / 24
		msg(color('gray').'--[ '.color('l').$player.color('gray').' ]-------------------------')
		msg(color('gray').'UUID: '.color('r').@pdata['uuid'])
		if(array_index_exists(@pdata, 'ban'), msg(color('red').'Banned '.color('r')
			.if(array_index_exists(@pdata['ban'], 'by'), 'by '.@pdata['ban']['by'].' ')
			.if(array_index_exists(@pdata['ban'], 'time'), 'temporarily ')
			.if(array_index_exists(@pdata['ban'], 'message'), '- "'.@pdata['ban']['message'].'"')))
		msg(color('gray').'Last Played: '.color('r').
		if(@days >= 3) {
			'Over '.floor(@days).' days ago'
		} else if(@hours >= 2) {
			'Over '.floor(@hours).' hours ago'
		} else {
			floor(@minutes).' minutes ago'
		})
		if(array_index_exists(@pdata, 'homeless'), msg(color('gray').'Homeless: '.color('r').'TRUE'))
		if(array_index_exists(@pdata, 'ips'), msg(color('gray').'IPs: '.color('r').array_implode(@pdata['ips'], ' ')))
	)
	if(array_index_exists(@pdata, 'approval'), msg(color('gray').'Approved by: '.color('r').@pdata['approval']))
	if(array_index_exists(@pdata, 'coins'), msg(color('gray').'Coins: '.color('r').@pdata['coins']))
	if(array_index_exists(@pdata, 'support'),
		msg(color('gray').'Support: '.color('r').'$'.@pdata['support']))
<<<

#Database control
/sql $action $namespace [$] = >>>
	if(!pisop(), die('You do not have permission.'))
	@data = get_values($namespace)
	if(array_size(@data) == 0, die('No keys for that namespace.'))
	switch($action) {
		case 'delete':
			foreach(@key: @value in @data) {
				clear_value(@key)
			}
			msg('Deleted '.$namespace)
		case 'rename':
			foreach(@key: @value in @data) {
				store_value(reg_replace($namespace, $, @key), @data[@key])
				clear_value(@key)
			}
		case 'indexchild':
			foreach(@key: @value in @data) {
				@array[$] = @value
				store_value(reg_replace('.'.$, '', @key), @array)
				clear_value(@key)
			}
	}
<<<

*:/matchip $ip = >>>
	if(!has_permission('group.moderators'), die('You do not have permission.'))
	@players = get_values('players')
	msg(color('bold').'Matched Players for IP: '.$ip)
	foreach(@key: @player in @players) {
		if(array_index_exists(@player, 'ips') && array_contains(@player['ips'], $ip)) {
			msg('Matched: '.split('.', @key)[1])
		}
	}
<<<

*:/money [$] = call_alias('/coins '.$)
*:/bal [$] = call_alias('/coins '.$)
*:/coins [$action] [$player] [$amount] = >>>
	if(!$action) {
		msg(color('gold').round(_acc_balance(player())).' coins')
	} else {
		if(!has_permission('group.moderators'), die('You do not have permission.'))
		if(!$player, die('Usage: /coins <add|sub|info> <player> <amount>'))
		switch($action) {
		case 'add':
		case 'give':
			if(!$amount, die('Usage: /coins <add|sub|info> <player> <amount>'))
			if(_acc_add($player, $amount)) {
				msg('Gave '.$player.' '.$amount.' coins.')
			} else {
				msg('Can\'t find player.')
			}
		case 'sub':
		case 'subtract':
		case 'remove':
			if(!$amount, die('Usage: /coins <add|sub|info> <player> <amount>'))
			if(_acc_subtract($player, $amount)) {
				msg('Subtracted '.$amount.' coins from '.$player)
			} else {
				msg('Can\'t find player.')
			}
		case 'info':
			if(@coins = _acc_balance($player)) {
				msg(color('gold').round(_acc_balance($player)).' coins')
			} else {
				msg('Can\'t find player.')
			}
		}
	}
<<<