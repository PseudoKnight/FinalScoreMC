*:/inv convert [$player] [$world] = >>>
	if(!has_permission('group.admin')) {
		die('You do not have permission.');
	}

	include('util.library/convert.ms');

	if($player) {
		@endpoint = '';
		if(!$world) {
			@endpoint = 'groups/default/'.$player.'.yml';
		} else {
			@endpoint = 'worlds/'.$world.'/'.$player.'.yml';
		}
		@original = yml_decode(read('../../../../Multiverse-Inventories/'.@endpoint))['SURVIVAL'];
		try {
			@inv = array();
			if(array_index_exists(@original, 'inventoryContents')) {
				@inv = _json_to_array(json_decode(@original['inventoryContents']));
			}
			if(array_index_exists(@original, 'armorContents')) {
				@armor = _json_to_array(json_decode(@original['armorContents']));
				foreach(@key: @value in @armor) {
					@inv[@key + 36] = @value
				}
			}
			deserialize_pinv(yml_encode(@inv));
			@inv = array();
			if(array_index_exists(@original, 'enderChestContents')) {
				@inv = _json_to_array(json_decode(@original['enderChestContents']));
				deserialize_penderchest(yml_encode(@inv));
			}
		} catch(Exception @json) {
			try {
				@inv = array();

				if(array_index_exists(@original, 'inventoryContents')) {
					@inv = _custom_to_array(@original['inventoryContents']);
				}
				if(array_index_exists(@original, 'armorContents') && @original['armorContents']) {
					@armor = _custom_to_array(@original['armorContents']);
					foreach(@key: @value in @armor) {
						@inv[@key + 36] = @value
					}
				}
				deserialize_pinv(yml_encode(@inv));
				@inv = array();
				if(array_index_exists(@original, 'enderChestContents')) {
					@inv = _custom_to_array(@original['enderChestContents']);
					deserialize_penderchest(yml_encode(@inv));
				}
			} catch(Exception @custom) {
				msg('Failed to process '.$player);
				msg(@json['message']);
				msg(@json['stackTrace']);
				msg(@custom['message']);
				msg(@custom['stackTrace']);
			}
		}
	} else {
		@processed = 0;
		@total = 0;
		foreach(@key: @pdata in get_values('uuids')) {

			if(!array_index_exists(@pdata, 'name')) {
				# possibly whitelisted, but not joined, skip
				continue();
			}
			@original = '';
			@name = @pdata['name'];
			@endpoint = 'groups/default/'.@pdata['name'].'.yml';
			try {
				@original = yml_decode(read('../../../../Multiverse-Inventories/'.@endpoint))['SURVIVAL'];
			} catch(Exception @e) {
				# no valid data... check for files with aliases
				if(array_index_exists(@pdata, 'names')) {
					foreach(@name in @pdata['names']) {
						@endpoint = 'groups/default/'.@name.'.yml';
						try {
							@original = yml_decode(read('../../../../Multiverse-Inventories/'.@endpoint))['SURVIVAL'];
							# found one, but keep looking for the latest
						} catch(Exception @e) {
							continue();
						}
					}
					if(!is_array(@original)) {
						# no files for aliases found
						continue();
					}
				} else {
					# no aliases to work with, skip
					continue();
				}
			}
			@total++;
			@world = '';
			try {
				@world = yml_decode(read('../../../../Multiverse-Inventories/players/'.@name.'.yml'))['playerData']['lastWorld'];
				if(_is_survival_world(@world)) {
					continue();
				}
			} catch(IOException @ex) {
				# no world
			}
			@pinv = associative_array();
			@ender = associative_array();
			@stats = associative_array();
			@effects = associative_array();
			@bed = array();
			try {
				if(array_index_exists(@original, 'inventoryContents')) {
					@pinv = _json_to_array(json_decode(@original['inventoryContents']));
				}
				if(array_index_exists(@original, 'armorContents')) {
					@armor = _json_to_array(json_decode(@original['armorContents']));
					foreach(@k: @value in @armor) {
						@pinv[@k + 36] = @value
					}
				}

				if(array_index_exists(@original, 'enderChestContents')) {
					@ender = _json_to_array(json_decode(@original['enderChestContents']));
				}

				if(array_index_exists(@original, 'stats') && length(@original['stats']) > 4) {
					@stats = _parse_json_stats(json_decode(@original['stats']));
				}

				if(array_index_exists(@original, 'potions') && length(@original['potions']) > 4) {
					@effects = _parse_json_potions(json_decode(@original['potions']));
				}

				if(array_index_exists(@original, 'bedSpawnLocation') && length(@original['bedSpawnLocation']) > 4) {
					@bed = _parse_json_bed(json_decode(@original['bedSpawnLocation']));
				}

			} catch(Exception @json) {
				try {
					if(array_index_exists(@original, 'inventoryContents')) {
						@pinv = _custom_to_array(@original['inventoryContents']);
					}
					if(array_index_exists(@original, 'armorContents') && @original['armorContents']) {
						@armor = _custom_to_array(@original['armorContents']);
						foreach(@k: @value in @armor) {
							@pinv[@k + 36] = @value
						}
					}

					if(array_index_exists(@original, 'enderChestContents')) {
						@ender = _custom_to_array(@original['enderChestContents']);
					}

					if(array_index_exists(@original, 'stats') && length(@original['stats']) > 4) {
						@stats = _parse_custom_stats(@original['stats']);
					}

					if(array_index_exists(@original, 'potions') && length(@original['potions']) > 4) {
						@effects = _parse_custom_potions(@original['potions']);
					}

					if(array_index_exists(@original, 'bedSpawnLocation') && length(@original['bedSpawnLocation']) > 4) {
						@bed = _parse_custom_bed(@original['bedSpawnLocation']);
					}

				} catch(Exception @custom) {
					msg('Failed to process '.@pdata['name']);
					msg(@json['message']);
					msg(@json['stackTrace']);
					msg(@custom['message']);
					msg(@custom['stackTrace']);
					continue();
				}
			}

			@pdata['survival'] = @stats;
			if(@pinv) {
				@pdata['survival']['inv'] = yml_encode(@pinv);
				@processed++;
			}
			if(@ender) {
				@pdata['survival']['ender'] = yml_encode(@ender);
			}
			if(@effects) {
				@pdata['survival']['effects'] = @effects;
			}
			if(@bed) {
				@pdata['survival']['bed'] = @bed;
			}
			if(@world) {
				@pdata['world'] = @world;
			}
			/*
			if(array_index_exists(@pdata, 'lastloc')) {
				if(array_index_exists(@pdata['lastloc'], 'survival')) {
					@pdata['survival']['loc'] = @pdata['lastloc']['survival'];
				}
				if(array_index_exists(@pdata['lastloc'], 'custom')) {
					@pdata['adventure']['loc'] = @pdata['lastloc']['custom'];
				}
				if(array_index_exists(@pdata['lastloc'], 'dev')) {
					@pdata['creative']['loc'] = @pdata['lastloc']['dev'];
				}
				array_remove(@pdata, 'lastloc');
			}
			*/
			store_value(@key, @pdata);
			action_msg(@pdata['name']);
		}
		msg('Found and converted '.@processed.' inventories of '.@total.'!');
	}
<<<
