*:/slimeygolf = >>>
	include('basic.library/course.ms');
	include('basic.library/player.ms');
	include('basic.library/ball.ms');
	
	if(!(@loc = get_command_block())) {
		@loc = ploc();
	}
	if(_is_survival_world(@loc['world'])) {
		die('Not allowed in this world.');
	}
	
	if(!(@course = _get_course(@loc))) {
		die('No Slimey Golf course here.');
	}
	
	if(array_contains(get_scoreboards(), @course)) {
		die('This game is already active.')
	}
	
	@golf = array(
		course: array(name: @course, loc: array(0, 0, 0, @loc['world']), par: 0), 
		players: array(),
	);
	
	create_scoreboard(@course);
	create_objective('strokes', 'DUMMY', @course);
	set_objective_display('strokes', array(displayname: color('a').'STROKES (PAR: 0)', slot: 'SIDEBAR'), @course);
	
	foreach(@player in all_players(@loc['world'])) {
		if(array_contains(sk_current_regions(@player), @course)
		&& _add_player(@player, @golf)
		&& array_size(@golf['players']) >= 4) {
			break();
		}
	}
	
	if(array_size(@golf['players']) == 0) {
		remove_scoreboard(@course);
		die('No players in "slimeygolf" region.');
	}
	
	# Physics defaults
	@friction = 0.95;
	@bounciness = 0.75;
	
	bind(player_interact, array(id: @course), array(item: 341, button: 'right', block: 159), @event, 
	@golf = @golf) {
		if(array_index_exists(@golf['players'], player())
		&& @event['action'] == 'right_click_block'
		&& @event['facing'] == 'up'
		&& split(':', get_block_at(@event['location']))[1] == @golf['players'][player()]['hole'] + 5) {
		
			@ball = @golf['players'][player()];
		
			set_pinv(player(), array('null': null));
			set_pexp(player(), 0);
			
			if(_ball_exists(@ball['id'])) {
				_remove_ball(@ball['id']);
				unbind('golf_swing_'.player());
				unbind('golf_hit_'.player());
			} else {
				set_plevel(player(), 0);
			}
			
			@loc = _relative(@event['location'], 'up');
			if(@ball['type'] == 0 || @ball['type'] == 2) {
				@mobtype = 'SLIME';
				@mobcheck = 'SLIME';
			} else {
				@mobtype = 'MAGMACUBE';
				@mobcheck = 'MAGMA_CUBE';
			}
			@loc['x'] += 0.5;
			@loc['z'] += 0.5;
			
			_place_ball(@mobtype, @loc, @ball);
			
			@lowesthole = 10;
			foreach(@player in @golf['players']) {
				if(@player['hole'] < @lowesthole) {
					@lowesthole = @player['hole'];
				}
			}
			if(@lowesthole == @ball['hole']) {
				@golf['course']['loc'][0] = @event['location']['x'];
				@golf['course']['loc'][1] = @event['location']['y'] - 2;
				@golf['course']['loc'][2] = @event['location']['z'];
				set_block_at(@golf['course']['loc'], 152);
			} else {
				set_block_at(_relative(@event['location'], 'down', 2), 152);
			}
			
			# par for this hole
			if(!array_index_exists(@golf['course'], @ball['hole'])) {
				@par = split(':', get_block_at(_relative(@event['location'], 'down', 1)))[1];
				@golf['course'][@ball['hole']] = @par;
				@golf['course']['par'] += @par;
				set_objective_display('strokes', array(displayname: color(10).'STROKES (PAR: '.@golf['course']['par'].')',
					slot: 'SIDEBAR'), @golf['course']['name']);
			}
			
			bind(player_interact_entity, array(id: 'golf_swing_'.player()), array(clicked: @mobcheck), @event, 
			@player = player(), @golf = @golf) {
				if(@player != player(), die());
				@ball = @golf['players'][player()];
				if(@event['id'] != @ball['id'], die());
				cancel();
				if(!@ball['swing']) {
					set_pexp(0);
					@ball['swing'] = true;
					set_interval(50, closure(){
						if(pexp() < 115 && @ball['swing']) {
							set_pexp(pexp() + 5);
						} else {
							clear_task();
							@ball['swing'] = false;
							set_pexp(0);
						}
					});
				}
			}
			
			bind(entity_damage, array(id: 'golf_hit_'.player()), array(id: @ball['id'], cause: 'ENTITY_ATTACK'), @event, 
			@golf = @golf) {
				@xp = pexp(@event['damager']);
				@player = @event['damager'];
				@ball = @golf['players'][@player];
				if(@xp > 0 && @ball['swing']
				&& @ball['id'] == @event['id']) {
					@ball['swing'] = false;
					@xp -= 10;
					set_pexp(@player, @xp);
					set_plevel(@player, plevel(@player) + 1);
					set_pscore('strokes', @player, get_pscore('strokes', @player, @golf['course']['name']) + 1, 
						@golf['course']['name']);
					set_timeout(50, closure(){
						@v = entity_velocity(@event['id']);
						@v['x'] = @v['x'] * (3 * (@xp / 100));
						@v['z'] = @v['z'] * (3 * (@xp / 100));
						@ball['velocity'] = array('x': @v['x'], 'y': 0, 'z': @v['z']);
						set_entity_velocity(@event['id'], @ball['velocity']);
					})
					set_pinv(@player, array(0: array('type': 341)));
				} else {
					cancel();
				}
				# cancel()
				# @ploc = ploc(@event['damager'])
				# @eloc = entity_loc(@event['id'])
				# @x = (@eloc['x'] - @ploc['x']) * (@xp / 200)
				# @z = (@eloc['z'] - @ploc['z']) * (@xp / 200)
				# set_entity_velocity(@event['id'], array(@x, 0, @z))
			}
		}
	}
	
	bind(target_player, array('id': @course.'_target'), null, @event, @golf = @golf) {
		if((@event['mobtype'] == 'SLIME' || @event['mobtype'] == 'MAGMA_CUBE')
		&& (!array_index_exists(@golf['players'], @event['player'])
		|| @golf['players'][@event['player']]['id'] != @event['id'])) {
			foreach(@player: @ball in @golf['players']) {
				if(@ball['id'] == @event['id']) {
					modify_event('player', @player);
					break();
				}
			}
		}
	}
	
	set_interval(50, closure(){
		foreach(@player: @ball in @golf['players']) {
			if(_ball_exists(@ball['id'])) {
				@setvelocity = true;
				@v = entity_velocity(@ball['id']);
				@loc = entity_loc(@ball['id']);
				@loc['y'] -= 1;
				@loc[1] -= 1;
				if(@v['x'] == 0) {
					@v['x'] = 0 - @ball['velocity']['x'] * @bounciness;
				} else if(abs(@ball['velocity']['x'] - @v['x']) < 0.1) {
					@v['x'] = @ball['velocity']['x'] * @friction;
				}
				@block = get_block_at(@loc);
				if(@block[0] != '0' && @v['y'] >= -0.0784000015258789) {
					if(@loc['y'] - floor(@loc['y']) == 0) {
						# if ball dropped into the hole
						if(@block == '35:15') {
							# check for birdie/eagle
							@hits = plevel(@player);
							@par = @golf['course'][@ball['hole']];
							if(@hits == @par - 1) {
								# BIRDIE
								launch_firework(@loc, 
									array(strength: 0, trail: true, type: 'BURST'));
								play_sound(ploc(@player), array(sound: 'VILLAGER_YES'));
							} else if(@hits <= @par - 2) {
								# EAGLE
								launch_firework(@loc, 
									array(strength: 0, trail: true, type: 'BALL_LARGE'));
								play_sound(ploc(@player), array(sound: 'VILLAGER_YES'));
							} else if(@hits > @par) {
								play_sound(ploc(@player), array(sound: 'VILLAGER_NO'));
							}
						
							# clean up the hole for this player
							_remove_ball(@ball['id']);
							unbind('golf_swing_'.@player);
							unbind('golf_hit_'.@player);
							
							# check if last player to leave hole
							@lastplayer = true;
							foreach(@p: @b in @golf['players']) {
								if(@p != @player
								&& @b['hole'] <= @ball['hole']) {
									@lastplayer = false;
									break();
								}
							}
							if(@lastplayer) {
								set_block_at(@golf['course']['loc'], 0);
								# check if last hole
								if(@ball['hole'] == 9) {
									@end = true;
									foreach(@p: @b in @golf['players']) {
										if(@b['hole'] <= 9 && @p != @player) {
											@end = false;
											break();
										}
									}
									if(@end) {
										@loweststrokes = 999;
										@winner = '';
										foreach(@p: @b in @golf['players']) {
											if(ponline(@p)) {
												if(function_exists('set_collides_with_entities')) {
													set_collides_with_entities(@p, true);
												}
												@world = pworld(@p);
												if(@world == 'dev' || @world == 'custom') {
													_clear_pinv(@p);
												}
												@strokes = get_pscore('strokes', @p, @course);
												if(@strokes < @loweststrokes) {
													@winner = @p;
													@loweststrokes = @strokes;
												}
											}
										}
										
										_regionmsg(@course, _colorname(@winner).@winner.color('r').' is the winner!');
										unbind(@course);
										unbind(@course.'_target')
										set_timeout(10000, closure(){
											remove_scoreboard(@course);
										})
									}
								}
							}
							
							# prepare player for next hole
							@ball['hole'] += 1;
							@ball['id'] = 0;
							@setvelocity = false;
						}
						@v['y'] = 0;
					} else if(@v['y'] > 0) {
						@loc['y'] = ceil(@loc['y']);
						@loc[1] = ceil(@loc[1]);
						set_entity_loc(@ball['id'], @loc);
						@v['y'] = 0;
					}
				} else if(@block == '35:15') {
					play_sound(@loc, array('sound': 'WOOD_CLICK', 'pitch': 0.5 + @loc['y'] - floor(@loc['y'])));
				}
				
				if(@setvelocity) {
					if(@v['z'] == 0) {
						@v['z'] = 0 - @ball['velocity']['z'] * @bounciness;
					} else if(abs(@ball['velocity']['z'] - @v['z']) < 0.1) {
						@v['z'] = @ball['velocity']['z'] * @friction;
					}
					@ball['velocity'] = array('x': @v['x'], 'y': @v['y'], 'z': @v['z']);
					set_entity_velocity(@ball['id'], @ball['velocity']);
					if(@ball['type'] >= 2) {
						play_entity_effect(@ball['id'], 'HURT');
					}
				}
			}
		} else {
			clear_task();
		}
	})
	
	set_interval(10000, closure(){
		foreach(@player: @ball in @golf['players']) {
			if(!ponline(@player)
			|| !array_contains(sk_current_regions(@player), @course)) {
				_remove_player(@player, @golf);
				_remove_ball(@ball['id']);
			}
		}
		if(array_size(@golf['players']) == 0) {
			unbind(@course);
			unbind(@course.'_target');
			if(array_contains(get_scoreboards(), @course)) {
				remove_scoreboard(@course);
			}
			clear_task();
		}
	})
<<<