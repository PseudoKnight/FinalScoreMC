#########
# CLUCK #
#########

*:/cluck [$cmd] = >>>
switch($cmd) {
case 'reset':
	if(!has_permission('group.moderators'), die('You do not have permission.'))
	store_value('cluck', associative_array(
		'top': array(),
		'date': simple_date('MMM d')
	))

case 'top10':
	@scores = get_value('cluck')
	msg(color('l').'TOP 10 CLUCK PLAYERS')
	msg(color('7').'Since '.@scores['date'])
	for(@i = 0, @i < array_size(@scores['top']), @i++) {
		msg(if(length(@scores['top'][@i]['score']) < 2, '0').@scores['top'][@i]['score'].' - '.@scores['top'][@i]['name'])
	}

case 'startround':
	if(pworld() != 'custom', die())

	include('includes.library/cluck.ms')
	@cluck = import('cluck')
	if(!@cluck, @cluck = array('player': array('', ''), 'active': 0))

	if(@cluck['active'] + 45000 > time(), die('Already in progress.'))
	
	foreach(@p in array(0, 1)) {
		if(@cluck['player'][@p]
		&& (!ponline(@cluck['player'][@p]) 
		|| !array_contains(sk_current_regions(@cluck['player'][@p]), 'cluck'))) {
			@cluck['player'][@p] = ''
		}
	}
	
	if(!@cluck['player'][0] && !@cluck['player'][1]) {
		@cluck = _cluck_defaults()
		foreach(@p in all_players('custom')) {
			if(!ponline(@p), continue())
			@regions = sk_current_regions(@p)
			if(array_contains(@regions, 'cluck0')) {
				@cluck['player'][0] = @p
				continue()
			} else if(array_contains(@regions, 'cluck1')) {
				@cluck['player'][1] = @p
				continue()
			}
		}
	} else if(@cluck['player'][0] != player()
	&& @cluck['player'][1] != player()) {
		die('Only players can start a new round.')
	}
	
	@cluck['active'] = time()
    export('cluck', @cluck)
    _cluck_startround()
	
default:
	msg(color('gold').'[Cluck] You can play Cluck in Frog Park just outside of the event house')
	msg(color('gold').'[Cluck] '.color('r').'/cluck top10 '.color('gray').'List the top 10 high scores for Cluck')
	msg(color('gold').'[Cluck] '.color('r').'/cluck start '.color('gray').'Start a round of Cluck')
	msg(color('gold').'[Cluck] '.color('r').'/cluck reset '.color('gray').'(restricted) Resets the high scores')
}
<<<