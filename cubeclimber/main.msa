# CubeClimber by PseudoKnight (inspired by the DeathCube plugin)
# Requires by default a WorldGuard region named 'cubeclimber' for a buffer space (including the ground)
# and a region named 'cubeclimber_blocks' for the block generation / gameplay space
# If you want to use this on your server, please edit the _cc_reward() procedure to reward players how you want.
*:/cc [$] = call_alias('/cubeclimber' $)
*:/cubeclimber [$action] = >>>
	switch($action
	, 'start',
	
		if(!sk_region_exists(pworld(), 'cubeclimber')) { 
			die('Define the \'cubeclimber\' region first.') 
		}
		
		if(!sk_region_exists(pworld(), 'cubeclimber_blocks')) { 
			die('Define the \'cubeclimber_blocks\' region first.') 
		}
		
		@count = 0
		foreach(all_players(pworld()), @p,
			if(array_contains(sk_current_regions(@p), 'cubeclimber')) {
				@count++
			}
		)
		
		if(@count < 2) {
			die('You need to have at least 2 players to start CubeClimber.')
		}
		
		if(!(@cc = import('cubeclimber'))) {
			@cc = array(
				'players': array(),
				'highest': 0,
			)
			export('cubeclimber', @cc)
			
			foreach(all_players(pworld()), @p) {
				tmsg(@p, color('white').player().' queued up a game of '.color(7).'['.color(6).'Cube'.color('c').'Climber'.color(7).']')
			}
		} else {
			die('Already running.')
		}
		
		if(!array_contains(get_scoreboards(), 'cc')) {
			create_scoreboard('cc')
			create_objective('height', 'DUMMY', 'cc')
			set_objective_display('height', array('slot': 'SIDEBAR', 'displayname': 'Starting...'), 'cc')
		}
		
		include('includes.library/cubeclimber.ms')
		_cc_start()
		
	, 'reset',
		if(!has_permission('cubeclimber.reset'), die('You do not have permission.'))
		@cc = import('cubeclimber')
		if(@cc) {
			clear_task(@cc['interval'])
		}
		if(array_contains(get_scoreboards(), 'cc')) {
			foreach(all_players(), @p) {
				if(get_pscoreboard(@p) == 'cc') {
					set_pscoreboard(@p)
				}
			}
			remove_scoreboard('cc')
		}
		export('cubeclimber', null)
		msg('CubeClimber reset.')
		
	, 
		msg(color(7).'['.color(6).'Cube'.color('c').'Climber'.color(7).'] '
		.'is a minigame where the goal is to reach the top of the block tower first. '
		.'Blocks that you walk on will change to your randomly assigned color. You can '
		.'break blocks of that same color.')
		msg('/cubeclimber start '.color(7).'Start the game.')
		msg('/cubeclimber reset '.color(7).'Resets CubeClimber, if needed.')
		msg(color(7).'alias: '.color('r').'/cc [start|reset]')
	)
<<<