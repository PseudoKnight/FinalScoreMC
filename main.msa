*:/tip = >>>
	@tips = import(_world_group(pworld()).'-tips', array('Sometimes there\'s no more tips.'));
	@tip = @tips[rand(array_size(@tips))];
	msg(colorize('&7TIP: &r'.@tip));
<<<

*:/transfer = >>>
	msg('Transfers ownership of a tamed mob to another online player.\n'
		.'Usage: /transfer <player>');
<<<

*:/transfer $player = >>>
	@player = _find_player($player);
	bind(player_interact_entity, array('id': 'transfer'.player().@player), null, @e, @player, @commandsender = player()) {
		if(player() != @commandsender) {
			die();
		}
		cancel();
		unbind();
		@owner = get_mob_owner(@e['id']);
		if(!@owner) {
			die(color('red').'This mob has no owner or cannot be tamed!');
		}
		if(@owner != player() && !has_permission('group.moderator')) {
			die(color('red').'This '.entity_type(@e['id']).' belongs to '.@owner.'.');
		}
		tame_mob(@player, @e['id']);
		msg(color('green').'This '.entity_type(@e['id']).' now belongs to '.@player.'.');
		if(ponline(@player)) {
			tmsg(@player, color('green').player().' gave a '.entity_type(@e['id']).' to you.');
		}
	}
	msg(color('yellow').'Right-click the tamed mob you wish to give to '.@player.'.');
<<<

*:/share = >>>
	msg('Share horses with friends you trust.\n'
		.'Usage: /share <player> horses\n'
		.'Example: /share PseudoKnight horses');
<<<

*:/share $player $ = >>>
	@shareables = array('horses')
	@types = parse_args($)
	foreach(@type in @types) {
		if(!array_contains(@shareables, @type)) {
			die(color('red').'Unknown type: '.@type.'\nShareable: '.@shareables)
		}
	}
	@player = _find_player($player);
	export('share'.@player, array(
		'player': player(),
		'types': @types
	))
	msg(color('yellow').'Requested '.@player.' to share '.array_implode(@types, ' and ').'.')
	tmsg(@player, color('yellow').player().' requested to share '.array_implode(@types, ' and ').'. Type "/share confirm".')
<<<

*:/share confirm = >>>
	@share = import('share'.player())
	if(!@share, die('No shares to confirm.'))

	@pdata = _pdata(@share['player'])
	if(!array_index_exists(@pdata, 'shared'), @pdata['shared'] = associative_array())
	if(!array_index_exists(@pdata['shared'], player()), @pdata['shared'][player()] = array())
	foreach(@type in @share['types']) {
		array_push(@pdata['shared'][player()], @type)
	}
	_store_pdata(@share['player'], @pdata)
	tmsg(@share['player'], color('green').'Now sharing '.array_implode(@share['types'], ' and ').' with '.player().'.')

	@pdata = _pdata(player())
	if(!array_index_exists(@pdata, 'shared'), @pdata['shared'] = associative_array())
	if(!array_index_exists(@pdata['shared'], @share['player']), @pdata['shared'][@share['player']] = array())
	foreach(@type in @share['types']) {
		array_push(@pdata['shared'][@share['player']], @type)
	}
	_store_pdata(player(), @pdata)
	msg(color('green').'Now sharing '.array_implode(@share['types'], ' and ').' with '.@share['player'].'.')

	export('share'.player(), null)
<<<

*:/unshare = >>>
	msg('Undo previous shares.\n'
		.'Usage: /unshare <player> horses\n'
		.'Example: /unshare PseudoKnight horses');
<<<

*:/unshare $player $ = >>>
	@shareables = array('horses')
	@types = parse_args($)
	foreach(@type in @types) {
		if(!array_contains(@shareables, @type)) {
			die(color('red').'Unknown type: '.@type.'\nShareable: '.@shareables)
		}
	}
	try {
		@player = player($player);
	} catch(PlayerOfflineException @ex) {
		if(get_value('players', to_lower($player))) {
			@player = $player;
		} else {
			die('Unknown player.');
		}
	}

	@pdata = _pdata(player())
	if(!array_index_exists(@pdata, 'shared') || !array_index_exists(@pdata['shared'], @player)) {
		die(color('gold').'You are not sharing anything with this player.')
	}
	foreach(@type in @types) {
		if(!array_contains(@pdata['shared'][@player], @type)) {
			die(color('gold').'You are not sharing '.@type.' with '.@player)
		} else {
			array_remove_values(@pdata['shared'][@player], @type)
			if(array_size(@pdata['shared'][@player]) < 1) {
				array_remove(@pdata['shared'], @player)
				if(array_size(@pdata['shared']) < 1) {
					array_remove(@pdata, 'shared')
				}
			}
		}
	}
	_store_pdata(player(), @pdata)
	msg(color('green').'You are no longer sharing '.array_implode(@types, ' and ').' with '.@player.'.')

	@pdata = _pdata(@player)
	if(!array_index_exists(@pdata, 'shared') || !array_index_exists(@pdata['shared'], player())) {
		die(color('gold').'They are not sharing anything with you.')
	}
	foreach(@type in @types) {
		if(!array_contains(@pdata['shared'][player()], @type)) {
			die(color('gold').'They are not sharing '.@type.' with you.')
		} else {
			array_remove_values(@pdata['shared'][player()], @type)
			if(array_size(@pdata['shared'][player()]) < 1) {
				array_remove(@pdata['shared'], player())
				if(array_size(@pdata['shared']) < 1) {
					array_remove(@pdata, 'shared')
				}
			}
		}
	}
	_store_pdata(@player, @pdata)
	if(ponline(@player)) {
		tmsg(@player, color('green').player().' is no longer sharing '.array_implode(@types, ' and ').' with you.')
	}
<<<

#Runs the help procedure to list or display entries
*:/help [$selection='help'] = >>>
	@help = array(
		'rules':
			'\n- Do NOT break or take anything belonging to another player, unless otherwise specified.\n'.
			'- Do NOT harass, spam, or swear at people in chat.\n'.
			'- Do NOT actively use mods for the purpose of cheating.\n',
		'commands':
			'Get a list and description of useful commands at >>> http://finalscoremc.com/commands <<<',
		'homes':
			'You can have one home per world. Set them by typing "/sethome", then "/home" to teleport to them. '.
			'You can teleport to your homes on other worlds with "/home worldname", homes of other players with '.
			'"/home playername", or both wtih "/home worldname playername".',
		'privatemessages': 'messages',
		'messages':
			'Send a private message to another player with "/msg playername message" or "@playername message". '.
			'You can use "/r <message>" for a reply.',
		'spawns':
			'There is one central spawn on each world, including nethers. Type "/spawn" to teleport to the spawn of '.
			'the world you are in or follow it with a world name to teleport there.',
		'portal': 'startgates',
		'gates': 'stargates',
		'stargates':
			'Stargates are special portals you build to teleport within a network of locations. There is a more '.
			'detailed guide on the website. But to make one, first build the gate as seen (without sign or button), '.
			'place a sign on the correct location, then type the gate name on line one and the gate network name on '.
			'line three.',
		'privatechests': 'privatesigns',
		'privatedoors': 'privatesigns',
		'private': 'privatesigns',
		'privatesigns':
			'You can put a [private] sign on a chest, furnace, and the block above/below doors. The following lines '.
			'after [private] should contain the players you want to give access. Optionally "[Everyone]" provides '.
			'access to everyone, and "[Timer:4]" will close a door 4 seconds after it was opened.',
		'shops':
			'Chest shops allow players to buy and sell different items using currency. (g: gold, d: diamond, i: iron, '.
			'e: emerald) Right click the SIGN with the currency in hand to BUY. Shop owners can edit shop '.
			'rates with the "/shop edit <transaction line>" command. Players can search for available items with '.
			'"/shop list <item>". There\'s a detailed guide on the website.',
		'website':
			'The website is at >>> http://finalscoremc.com <<< There you can find a command reference, guides, '.
			'videos, live maps, and more.',
		'map': 'livemap',
		'livemap':
			'>>> http://mc.finalscoremc.com:25966 <<< The website has a handy live map that shows the '.
			'location of players and their homes, among other things.',
		'staff':
			'You can get help from anyone with a blue name. Orange players are Frog Park staff and can help there.',
		'owner':
			'Scottyd_Utah currently owns the domain name and Minecraft server hosting. PseudoKnight owns the '.
			'custom software, provides the website, and primarily runs the server.',
		'plugins':
			'Type "/plugins" for a full list of plugins installed. Most server functionality is written in '.
			'MethodScript using the CommandHelper plugin. You can find those scripts at '.
			'http://github.com/PseudoKnight/FinalScoreMC',
		'groups': 'permissions',
		'ranks': 'permissions',
		'permissions':
			'We currently have 8 permission groups: '.
			'default (white names; new un-added players), '.
			'member (yellow names; most players are in this group; minor restrictions), '.
			'regular (like member, but long time or otherwise trusted players), '.
			'donor (green names or diamonds; minor cosmetic bonuses), '.
			'builder (orange names; Frog Park staff; builds things for the park), '.
			'engineer (like builder, but with protected region override), '.
			'moderator (blue names; server staff), '.
			'and admin (like moderator, but has access to everything; aka op).',
		'mumble': 'voice',
		'discord': 'voice',
		'voice':
			'Everyone is welcome to use our voice chat servers. Our mumble server is at mum.finalscoremc.com '.
			'with port 64758. You can get Mumble @ \n>>> http://mumble.info <<<\n Also, we have a Discord '.
			'server @ \n>>> https://discordapp.com/invite/0aBtE3n1P6GixDwO <<<',
		'worldguard': 'regions',
		'regions':
			'Regions are protected zones where only owners and members can build or access chests. You can create '.
			'your own region by using a golden axe (or commands), left and right clicking to select a cuboid for '.
			'the region, then use "/rg claim <regionid>". More details are on the website.',
		'whitelist':
			'You can get on the whitelist by asking a staff member, or a friend who is a regular can do it using the '.
			'"/approve playername" command.',
		'survival': 'worlds',
		'worlds':
			'There are four primary worlds: Alpha (Plaza at spawn), Delta (Final City at spawn), Gamma (Gamma City '.
			'at spawn), and Omega. In addition, there are four respective nether dimensions and one End dimension. '.
			'You can type "/where" to see which worlds players are on.',
		'seeds': 'worldseeds',
		'worldseeds':
			'These are the seeds for the four main worlds -- Alpha: -621778714517541268, Delta: '.
			'-8443156782283251243, Gamma: -754633420005903009, Omega: 950483747. Outworld seeds are not provided.'
	);
	if($selection === 'help') {
		msg(color('a').'Get help on any of these topics:');
		@topics = array_filter(@help, closure(@key, @value){
			return(length(@value) > 16);
		});
		msg(color('6').array_implode(array_keys(@topics), ' '));
	} else {
		try {
			@title = to_lower($selection);
			@text = array_get(@help, @title, '');
			if(!@text) {
				@text = @help[@title.'s'];
			}
			if(length(@text) < 17) {
				@title = @text;
				@text = @help[@text];
			}
			msg(to_upper(@title).': '.color('a').@text);
		} catch(IndexOverflowException @ex) {
			msg(color('c').'Unknown entry. Type "/help" to find available entries.');
		}
	}
<<<
*:/rules = >>>
	broadcast(player().' read the rules.', 'group.moderator');
	call_alias('/help rules');
<<<

# PLAYER LISTS #
*:/who = >>>
	@list = array(array('admin', 'b', ''), array('moderator', 'b', ''), array('engineer', '6', ''), array('builder', '6', ''), array('donor', 'a', ''), array('regular', 'e', ''), array('member', 'e', ''), array('default', 'f', ''))
	@allplayers = all_players()
	foreach(@player in @allplayers) {
		for(@i = 0, @i < array_size(@list), @i++) {
			if(array_contains_ic(pgroup(@player), @list[@i][0])) {
				@list[@i][2] = @list[@i][2].' '.pinfo(@player, 4)
				break()
			}
		}
	}
	for(@i = 0, @i < array_size(@list), @i++) {
		if(@list[@i][2] !== '') {
			msg(color(@list[@i][1]).@list[@i][0].color(7).': '.@list[@i][2])
		}
	}
<<<
*:/list = >>>
	if(player() !== '~console') {
		call_alias('/who')
	}
<<<

*:/where = >>>
	@worlds = _worlds_config();
	@list = associative_array();
	foreach(@player in all_players()) {
		@world = @worlds[pworld(@player)]['name'];
		if(!array_index_exists(@list, @world)) {
			@list[@world] = array();
		}
		@list[@world][] = _colorname(@player).@player;
	}
	foreach(@world: @players in @list) {
		msg(@world.': '.array_implode(@players));
	}
<<<
*:/mvw = call_alias('/where')

# DICE ROLLING #
*:/roll [$sides=6] = >>>
	if($sides > 100, die(color('a').'[Dice] '.color('f').'There\'s a limit of 100 sides.'))
	if($sides < 2, die(color('a').'[Dice] '.color('f').'There\'s a minimum of 2 sides.'))
	if($sides == 2) {
		@message = color('a').'[Coin] '._colorname(player()).player().color('f').' flipped a coin and got '.if(rand(2) == 0, 'heads.', 'tails.')
	} else {
		@message = color('a').'[Dice] '._colorname(player()).player().color('f').' rolled a '.color('a').(rand($sides) + 1).' on a '.$sides.'-sided die.'
	}
	foreach(@p in all_players(pworld())) {
		if(!ponline(@p), continue())
		tmsg(@p, @message)
	}
<<<

# Colour codes
*:/colors = msg(colorize('&00&11&22&33&44&55&66&77&88&99&aa&bb&cc&dd&ee&ff &kk&r&ll&r&mm&r&nn&r&oo &rr'))

# Vanish
*:/v = >>>
	if(!has_permission('command.vanish')) {
		die('You do not have permission.');
	}
	if(pmode() == 'SPECTATOR') {
		@worlds = _worlds_config();
		if(extension_exists('CHDynmap')) {
			dm_set_pvisible(true);
		}
		set_pmode(@worlds[pworld()]['mode']);
	} else {
		if(extension_exists('CHDynmap')) {
			dm_set_pvisible(false);
		}
		set_pmode('SPECTATOR');
	}
<<<

# Custom join/leave messages
*:/enter [$] = >>>
	@msg = $;
	if(!@msg) {
		die('Enter a message to be displayed when entering the server.');
	} else if(length(@msg) > 88) {
		die('Enter messages must be 88 or less characters.');
	}

	@pdata = _pdata(player());
	@escapedMessage = replace(@msg, '\\', '\\\\');
	@escapedMessage = replace(@escapedMessage, '"', '\\u0022');
	@pdata['enter'] = @escapedMessage;
	_store_pdata(player(), @pdata);
	msg(colorize('&7Set server enter message to: '));
	@pre = '&8'.simple_date('h:mm').' &2\u00bb ';
	@name = _colorname().color('italic').player();
	include('/chat/includes.library/chat.ms');
	_tellraw(array(player()), array(array('colorize', @pre), array('hover', @name, 'Last played: Now'), '&e&o '.@escapedMessage));
<<<

*:/exit [$] = >>>
	@msg = $;
	if(!@msg) {
		die('Enter a message to be displayed when exiting the server.');
	} else if(length(@msg) > 88) {
		die('Exit messages must be 88 or less characters.');
	}

	@pdata = _pdata(player());
	@pdata['exit'] = @msg;
	_store_pdata(player(), @pdata);
	msg(colorize('&7Set server exit message to: '));
	msg(colorize('&8'.simple_date('h:mm').' &4\u00ab &o'._colorname().'&o'.player().'&e&o '.@msg));
<<<

*:/livemap [$player] = >>>
	@player = player();
	if($player) {
		@player = _find_player($player);
	}
	@loc = ploc(@player);
	@x = @loc['x'];
	@z = @loc['z'];
	@world = @loc['world'];
	@url = "http://mc.finalscoremc.com:25966/?worldname=@{world}&mapname=detailed&zoom=2&x=@{x}&y=64&z=@{z}";
	@msg = '["",{"text":"'.color('b').'[\u21D6 Go to Live Map]'.color('r').'","clickEvent":{"action":"open_url","value":"'.@url.'"}}]';
	runas('~console', '/tellraw '.player().' '.@msg);
<<<

*:/here = >>>
	@loc = ploc();
	@x = @loc['x'];
	@z = @loc['z'];
	@world = @loc['world'];
	@url = "http://mc.finalscoremc.com:25966/?worldname=@{world}&mapname=detailed&zoom=2&x=@{x}&y=64&z=@{z}";
	@msg = '["",{"text":"'.color('b').'[\u21D6 '.player().'\'s location on the Live Map]'.color('r').'","clickEvent":'
			.'{"action":"open_url","value":"'.@url.'"}}]';
	foreach(@player in all_players()) {
		runas('~console', "/tellraw @player @msg");
	}
<<<

*:/lastseen $player = >>>
	if(ponline($player)) {
		die('Um, right now!');
	}
	@uuid = _get_uuid(to_lower($player), true, false);
	@pdata = _pdata_by_uuid(replace(@uuid, '-', ''));
	@lastPlayed = plast_played(@uuid);
	@minutes = (time() - @lastPlayed) / 60000;
	@hours = @minutes / 60
	@days = @hours / 24
	msg(@pdata['name'].' was last seen '.
		if(@days > 14) {
			simple_date('MMMMM dd, YYYY', @lastPlayed)
		} else if(@days >= 3) {
			'over '.floor(@days).' days ago'
		} else if(@hours >= 2) {
			'over '.floor(@hours).' hours ago'
		} else {
			floor(@minutes).' minutes ago'
		}
	);
<<<
