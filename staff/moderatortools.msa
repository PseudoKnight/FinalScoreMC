/**
 * @command
 * @permission worldedit.remove
 */
*:/removeonly [$type] [$] = >>>
	if(!has_permission('worldedit.remove')) {
		die('No permission.');
	}
	if(!$type) {
		die('Usage: /removeonly [entityType] [radius=32] [-n]\n'
			.'Removes only a specific type of entity.\n'
			.'This will preserve named mobs, except when given the -n flag.\n'
			.'Not undoable!');
	}
	@type = $type;
	@removenamed = false;
	@radius = 32;
	@loc = entity_loc(puuid());
	foreach(@arg in parse_args($)) {
		if(is_numeric(@arg)) {
			@radius = min(@arg, 128);
		} else if(@arg === '-n') {
			@removenamed = true;
		}
	}
	if(is_null(@type)) {
		die('Expected an entity type as an argument.');
	}
	@entities = entities_in_radius(@loc, @radius, @type);
	@count = 0;
	foreach(@e in @entities) {
		if(@removenamed || !is_entity_living(@e) || !get_mob_name(@e)) {
			entity_remove(@e);
			@count++;
		}
	}
	msg('Removed '.@count.' of '.array_size(@entities).' in a '.@radius.' radius.');
<<<

*:/ban [$player] [$] = >>>
	if(!has_permission('group.moderator'), die('You do not have permission.'))
	if(!$player, die('Usage: /ban <player> [-t 1d-8h-10m] [message]'))
	@pdata = _pdata($player)
	@bantime = null
	@message = null
	if($) {
		@args = parse_args($)
		if(@args[0] === '-t') {
			@times = split('-', @args[1])
			@bantime = time()
			foreach(@t in @times) {
				switch(substr(@t, length(@t) - 1, length(@t))) {
				case 'd':
					@bantime = @bantime + (86400000 * substr(@t, 0, length(@t) - 1))
				case 'h':
					@bantime = @bantime + (3600000 * substr(@t, 0, length(@t) - 1))
				case 'm':
					@bantime = @bantime + (60000 * substr(@t, 0, length(@t) - 1))
				}
			}
			if(array_size(@args) > 2) {
				@message = array_implode(@args[cslice(2, array_size(@args) - 1)], ' ')
			}
		} else {
			@message = $
		}
	}
	@pdata['ban'] = associative_array()
	if(@bantime, @pdata['ban']['time'] = @bantime)
	if(@message, @pdata['ban']['message'] = @message)
	if(player() !== '~console', @pdata['ban']['by'] = player())
	_store_pdata($player, @pdata)
	if(ponline($player), pkick($player, if(@message, @message, 'Banned. Appeal at FinalScoreMC.com')))
	msg($player.' is now banned.')
	console(player().' banned '.$player.'.'.if(@message, ' (Reason: "'.@message.'")'), false);
<<<
