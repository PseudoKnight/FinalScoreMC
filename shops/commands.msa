*:/shop = >>>
	msg(color(6).'[Shop] Commands to list and edit shop signs.');
	msg('/shop edit <transaction> '.color('gray').'Edits a line on your shop');
	msg('Example: /shop edit buy 1 for 1g');
	msg('/shop list <item> '.color('gray').'Lists stocked shops for that item');
	msg('Example: /shop list diamond');
<<<

*:/shop edit = >>>
	msg(color(6).'[Shop] Edits a line on your shop that you\'re looking at.');
	msg('Example: '.color('gray').'"/shop edit buy 1 for 1g"');
<<<

*:/shop edit $transaction $ = >>>
	include('includes.library/shops.ms');
	if(length($) > 11) {
		die(color(6).'[Shop] There is a 15 character limit per line.');
	}
	
	@line = 0;
	if($transaction === 'buy') {
		@line = 1;
	} else if($transaction === 'sell') {
		@line = 2;
	} else {
		die(color(6).'[Shop] You need to specify "buy" or "sell".');
	}
	
	@loc = pcursor();
	
	if(!@shop = _get_shop(@loc)) {
		die(color(6).'[Shop] There is no shop sign there.');
	}
	
	if(!_is_shop_owner(player(), @shop['owner']) && !has_permission('shop.admin')) {
		die(color(6).'[Shop] You do not own this shop.');
	}
	
	@signText = get_sign_text(@loc);
	@signText[@line] = if(@line == 1, 'Buy ', 'Sell ').$;
	@shop[$transaction] = _get_price(@signText[@line]);
	if(!@shop[$transaction]) {
		die(color(6).'[Shop] Usage Example: /shop edit buy 16 for 1g');
	}
	set_sign_text(@loc, @signText);
	msg(color(6).'[Shop] Modified');
		
	@shops = get_value('shop', @shop['key']);
	if(!@shops) {
		die();
	}
	
	@loc = array(round(@loc['x']), round(@loc['y']), round(@loc['z']), @loc['world']);

	@t = @line - 1;
	foreach(@i: @s in @shops[@t]) {
		if(@s['location'] == @loc) {
			if(@t == 0) {
				@count = _get_inventory_count(_relative(@loc, 'down'), @shop['item']);
				if(is_null(@count)) {
					die(color(6).'[Shop] No container below this shop sign.');
				}
				if(@count < @shop['buy'][2]) {
					array_remove(@shops[@t], @i);
				} else {
					@shops[@t][@i] = array(
						'location': @loc, 
						'price': @shop['buy'][1], 
						'owner': @shop['owner'], 
						'stock': @count,
					);
				}
			} else {
				switch(@shop['sell'][4]) {
					case 'g':
						@currency = '266';
					case 'i':
						@currency = '265';
					case 'd':
						@currency = '264';
					case 'e':
						@currency = '388';
					default:
						die(color(6).'[Shop] Unknown currency.');
				}
				@count = _get_inventory_count(_relative(@loc, 'down'), @currency.':0');
				if(is_null(@count)) {
					die(color(6).'[Shop] No container below this shop sign.');
				}
				if(@count < @shop['sell'][3]) {
					array_remove(@shops[@t], @i);
				} else {
					@shops[@t][@i] = array(
						'location': @loc, 
						'price': @shop['sell'][1], 
						'owner': @shop['owner'], 
						'stock': @count
					);
				}
			}
			break();
		}
	}
	store_value('shop', @shop['key'], @shops);
<<<

*:/shop list = >>>
	msg(color(6).'[Shop] Lists stocked shops for an item.');
	msg('Example: '.color('gray').'"/shop list diamond"');
	msg('The location of each shop is shown as (x,y,z world) coordinates.');
	msg('You can see your current coordinates by pressing F3.');
<<<

# List available shop items
*:/shop list $ = >>>
	include('includes.library/shops.ms');
	@item = _data_values($);
	if(!@item) {
		die('Unknown item');
	}
	@itemid = replace(@item, ':', '.');
	@shops = get_value('shop', @itemid);
	if(!@shops) { 
		die(color('gold').'No stocked shops found for '.color('o').'"'.$.'" ('.@item.')');
	} else {
		msg(color('gold').'Shops you can buy or sell '.color('o').'"'.$.'"');
		msg(color('gray').'-----------------------------------------------------');
	}
	foreach(@key: @shop in @shops[0]) {
		msg(colorize('Buy '.@shop['price'].'&7 ('.@shop['stock'].') &e'.@shop['owner'].'&7 '
		.@shop['location'][0].','.@shop['location'][1].','.@shop['location'][2].' '._worldname(@shop['location'][3])));
	}
	msg(color('gray').'-----------------------------------------------------');
	foreach(@key: @shop in @shops[1]) {
		msg(colorize('Sell '.@shop['price'].'&7 ('.@shop['stock'].') &e'.@shop['owner'].'&7 '
		.@shop['location'][0].','.@shop['location'][1].','.@shop['location'][2].' '._worldname(@shop['location'][3])));
	}
	msg(color('gray').'-----------------------------------------------------');
<<<