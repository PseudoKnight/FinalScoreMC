*:/pvp [$action] [$id] [$team] = >>>
	switch($action
	, 'join',
		@pvp = import('pvp'.$id)
		@arena = get_value('arena.'.$id)
		if(!@arena, die('Can\'t find that arena.'))
		if(!@pvp) {
			@pvp = array('players': array(), 'running': 'false', 'cooldown': 0)
			@pvp['players'][player()] = array()
		} else {
			if(array_index_exists(@pvp['players'], player()), die('You already joined.'))
			if(@pvp['running'] == 'true' && 
			(array_index_exists(@pvp['arena'], 'lives')
			|| (array_index_exists(@arena, 'max') && array_size(@pvp['players']) >= @arena['max']))) {
				die('Match already in progress.')
			}
			@pvp['players'][player()] = array()
		}
		if(array_index_exists(@arena, 'mode') && array_contains(array('ctf', 'tdm', 'infection'), @arena['mode'])) {
			if(!array_index_exists(@pvp, 'team'), @pvp['team'] = array(array('players': array()), array('players': array())))
			if(array_size(@pvp['team'][0]['players']) * if(array_index_exists(@arena, 'teamratio'), @arena['teamratio'][1], 1) <= array_size(@pvp['team'][1]['players']) * if(array_index_exists(@arena, 'teamratio'), @arena['teamratio'][0], 1)) {
				@pvp['players'][player()]['team'] = 0
				array_push(@pvp['team'][0]['players'], player())
			} else {
				@pvp['players'][player()]['team'] = 1
				array_push(@pvp['team'][1]['players'], player())
			}
		}
		_worldmsg('custom', color('gray').'[PVP] '.color('r').player().' has joined '.to_upper($id).'. ('.array_size(@pvp['players']).')')
		console(player().' joined '.$id)
		export('pvp'.$id, @pvp)
		if(@pvp['running'] == 'true') {
			_pvp_initialize_players(@id, array(player()))
		} else if(array_index_exists(@arena, 'max') && array_size(@pvp['players']) >= @arena['max']) {
			call_alias('/pvp start '.$id)
		}
		
	, 'debug',
		@id = to_lower($id)
		@pvp = import('pvp'.@id)
		foreach(@pvp, @key, @value,
			msg(@key.': '.color('gray').@value)
		)

	, 'spectate',
		if(array_contains(get_scoreboards(), $id)) {
			set_pscoreboard(player(), $id)
		} else {
			msg('The arena isn\'t running.')
		}

	, 'start',
		@id = to_lower($id)
		@pvp = import('pvp'.@id)
		if(!@pvp, die('There is no match to start.'))
		if(@pvp['running'] == 'true', die('Match already in progress.'))
		
		@pvp['arena'] = get_value('arena.'.@id)
		if(!@pvp['arena'], die('Can\'t find that arena.'))
		
		if(player() == '~console' || pworld() != 'custom', die('You can only run this in Frog Park.'))

		# CHECK ARENA SETTINGS
		if(!array_index_exists(@pvp['arena'], 'lobby'), die('No lobby defined for arena.'))
		if(!array_index_exists(@pvp['arena'], 'region'), die('No region defined for arena.'))
		if(!array_index_exists(@pvp['arena'], 'spawn'), die('No spawns defined for arena.'))
		if(!array_index_exists(@pvp['arena'], 'min'), @pvp['arena']['min'] = 2)
		if(!array_index_exists(@pvp['arena'], 'broadcast'), @pvp['arena']['broadcast'] = @pvp['arena']['region'])
		if(!array_index_exists(@pvp['arena'], 'mode'), @pvp['arena']['mode'] = 'dm')
		if(!array_index_exists(@pvp['arena'], 'ff'), @pvp['arena']['ff'] = true)
		if(!array_index_exists(@pvp['arena'], 'flags'), @pvp['arena']['flags'] = array())
		if(!array_index_exists(@pvp['arena'], 'team') && array_contains(array('ctf', 'tdm', 'infection'), @pvp['arena']['mode'])) {
			@pvp['arena']['team'] = array(
				array('name': 'Red', 'color': 'c'), 
				array('name': 'Blue', 'color': '3')
			)
		}
		if(!array_index_exists(@pvp['arena'], 'score')) {
			switch(@pvp['arena']['mode']
			, 'ctf', @pvp['arena']['score'] = 5
			, 'koth', @pvp['arena']['score'] = 60
			)
		}
		
		if(array_contains(@pvp['arena']['flags'], 'debug') && !array_contains(pgroup(), 'builders'), die('This arena is still in testing and can only be started by Builders.'))
		
		foreach(array_keys(@pvp['players']), @player,
			if(!ponline(@player) || pworld(@player) != 'custom') {
				array_remove(@pvp['players'], @player)
			}
		)
		if(array_size(@pvp['players']) < @pvp['arena']['min'], die('There aren\'t enough players. ('.array_size(@pvp['players']).')'))
		if((@pvp['cooldown'] + 15000) > time() && !array_contains(pgroup(), 'builders')) {
			die('Arena still on a 15 second cooldown.')
		}
		
		if(array_contains(get_scoreboards(), @id), remove_scoreboard(@id))
		create_scoreboard(@id)
		
		# PREPARE TEAMS
		if(array_contains(array('tdm', 'ctf', 'infection'), @pvp['arena']['mode'])) {
			if(@pvp['arena']['mode'] == 'ctf') {
				@pvp['team'][0]['score'] = 0
				@pvp['team'][0]['flag'] = 0
				@pvp['team'][1]['score'] = 0
				@pvp['team'][1]['flag'] = 0
			}
			create_team(@pvp['arena']['team'][0]['name'], @id)
			create_team(@pvp['arena']['team'][1]['name'], @id)
			set_team_display(@pvp['arena']['team'][0]['name'], array('prefix': color(@pvp['arena']['team'][0]['color'])), @id)
			set_team_display(@pvp['arena']['team'][1]['name'], array('prefix': color(@pvp['arena']['team'][1]['color'])), @id)
			set_team_options(@pvp['arena']['team'][0]['name'], array('friendlyinvisibles': true), @id)
			set_team_options(@pvp['arena']['team'][1]['name'], array('friendlyinvisibles': true), @id)
		}

		switch(@pvp['arena']['mode']
		, 'koth',
			create_objective('seconds', 'DUMMY', @id)
			set_objective_display('seconds', array('displayname': color('a').'Seconds Left', 'slot': 'SIDEBAR'), @id)
		, array('tdm', 'dm', 'infection'),
			if(array_index_exists(@pvp['arena'], 'lives')) {
				create_objective('lives', 'DUMMY', @id)
				if(@pvp['arena']['lives'] > 1) {
					set_objective_display('lives', array('displayname': color('a').'Lives Left', 'slot': 'SIDEBAR'), @id)
				}
			}
		, 'ctf',
			create_objective('captures', 'DUMMY', @id)
			set_objective_display('captures', array('displayname': color('a').'Captures ('.@pvp['arena']['score'].')', 'slot': 'SIDEBAR'), @id)
			team_add_player(@pvp['arena']['team'][0]['name'], @pvp['arena']['team'][0]['name'], @id)
			set_pscore('captures', @pvp['arena']['team'][0]['name'], 0, @id)
			team_add_player(@pvp['arena']['team'][1]['name'], @pvp['arena']['team'][1]['name'], @id)
			set_pscore('captures', @pvp['arena']['team'][1]['name'], 0, @id)
		)

		foreach(array_keys(@pvp['players']), @player,
			if(pmode(@player) == 'CREATIVE', set_pmode(@player, 'SURVIVAL'))
		)

		@pvp['coins'] = array_size(@pvp['players']) - 2
		@pvp['binds'] = array()
		@pvp['running'] = 'true'
		
		# CLASSES
		if(array_index_exists(@pvp['arena'], 'classes')) {
			@size = array_size(@pvp['arena']['classes'][0])
			if(@size > 1) {
				@size = ceil(@size / 9) * 9
				create_virtualchest(array(
					'id': @id.'0',
					'size': @size,
					'title': 'Choose a Class'
				))
				for(@i = 0, @i < array_size(@pvp['arena']['classes'][0]), @i++,
					@class = get_value('class.'.@pvp['arena']['classes'][0][@i])
					if(!@class, continue())
					addto_virtualchest(@id.'0', 
						@class['selector']['type'].':'.@class['selector']['data'], 
						@class['selector']['qty'],
						@class['selector']['meta']
					)
				)
				foreach(@pvp['players'], @p, @pdata,
					if(!array_index_exists(@pdata, 'team') || @pdata['team'] == 0) {
						popen_virtualchest(@p, @id.'0')
					}
				)
			}
			@size = array_size(@pvp['arena']['classes'][1])
			if(@size > 1) {
				@size = ceil(@size / 9) * 9
				create_virtualchest(array(
					'id': @id.'1',
					'size': @size,
					'title': 'Choose a Class'
				))
				for(@i = 0, @i < array_size(@pvp['arena']['classes'][1]), @i++,
					@class = get_value('class.'.@pvp['arena']['classes'][1][@i])
					if(!@class, continue())
					addto_virtualchest(@id.'1', 
						@class['selector']['type'].':'.@class['selector']['data'], 
						@class['selector']['qty'],
						@class['selector']['meta']
					)
				)
				foreach(@pvp['players'], @p, @pdata,
					if(!pget_virtualchest(@p) && @pdata['team'] == 1) {
						popen_virtualchest(@p, @id.'1')
					}
				)
			}
			if(array_size(@pvp['arena']['classes'][0]) > 1 || array_size(@pvp['arena']['classes'][1]) > 1) {
				array_push(@pvp['binds'], @id.'classselector')
				bind('inventory_click', array('id': @id.'classselector'), array('slottype': 'CONTAINER', 'clicktype': 'LEFT', 'action': 'PICKUP_ALL'), @e, assign(@id, @id),
					@pvp = import('pvp'.@id)
					if(!array_index_exists(@pvp['players'], player())
					|| !@e['slotitem']['meta']
					|| !@e['slotitem']['meta']['display']) {
						die()
					}
					@class = to_lower(strip_colors(@e['slotitem']['meta']['display']))
					if((!array_index_exists(@pvp['players'][player()], 'team')
					&& array_contains_ic(@pvp['arena']['classes'][0], @class))
					|| (array_index_exists(@pvp['players'][player()], 'team')
					&& array_contains_ic(@pvp['arena']['classes'][@pvp['players'][player()]['team']], @class))) {
						cancel()
						if(@pvp['arena']['captain'][@pvp['players'][player()]['team']] == @class) {
							clear_virtualchest(@id.@pvp['players'][player()]['team'], @e['slot'])
							@pvp['team'][@pvp['players'][player()]['team']]['captain'] = player()
						}
						@pvp['players'][player()]['class'] = @class
						close_pinv()
						export('pvp'.@id, @pvp)
					}
				)
			}
		}

		#ANNOUNCE
		_worldmsg('custom', color('gray').'[PVP] '.color('r').'STARTING '.to_upper(@id).'...')
		if(array_contains(array('tdm', 'ctf', 'infection'), @pvp['arena']['mode'])) {
			_worldmsg('custom', 
			color(@pvp['arena']['team'][0]['color']).'['.@pvp['arena']['team'][0]['name'].'] '.array_implode(@pvp['team'][0]['players'], ' ').'\n'.color('r').color(@pvp['arena']['team'][1]['color']).'['.@pvp['arena']['team'][1]['name'].'] '.array_implode(@pvp['team'][1]['players'], ' '))
		} else {
			_worldmsg('custom', array_implode(array_keys(@pvp['players']), ' vs '))
		}
		export('pvp'.@id, @pvp)
		play_sound(@pvp['arena']['lobby'], array('sound': 'WITHER_SPAWN', 'pitch': 1.5, 'volume': 3))
		
		#START ROUND
		set_interval(3000, closure(
			@go = 1
			foreach(array_keys(@pvp['players']), @p,
				if(ponline(@p) && pget_virtualchest(@p)) {
					@go = 0
				}
			)
			if(@go) {
				clear_task()
				_pvp_start_round(@id)
			}
		))
		
	, 'end',
		if(!get_command_block() && !has_permission('group.builders'), die(color('red').'You do not have permission.'))
		if(!$id || !$team, die(color('gold').'Usage: /pvp stop arenaName winningTeam#'))
		@pvp = import('pvp'.$id)
		if(!@pvp || !@pvp['running'], die(color('red').'Not running.'))
		_pvp_end_match($id, @pvp['team'][$team - 1]['players'])
		
	,
		msg('/pvp join <id> '.color('gray').'Join the match')
		msg('/pvp start <id> '.color('gray').'Start the match')
		msg('/pvp end <id> <team> '.color('gray').'End in favor of team')
		msg('/pvp spectate <id> '.color('gray').'Displays the scoreboard')
	)
<<<	