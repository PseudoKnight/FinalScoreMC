*:/pvp [$action] [$id] = >>>
	switch($action
	, 'join',
		@pvp = import('pvp'.$id)
		@arena = get_value('arena.'.$id)
		if(!@arena, die('Can\'t find that arena.'))
		if(!@pvp) {
			@pvp = array('players': array(), 'running': false, 'cooldown': 0)
			@pvp['players'][player()] = array()
		} else {
			if(array_index_exists(@pvp['players'], player()), die('You already joined.'))
			if(@pvp['running'] == true, die('Match already in progress.'))
			@pvp['players'][player()] = array()
		}
		_worldmsg('custom', color('gray').'[PVP] '.color(r).player().' has joined '.$id.'. ('.array_size(@pvp['players']).')')
		export('pvp'.$id, @pvp)
		if((array_index_exists(@arena, 'max') && array_size(@pvp['players']) >= @arena['max'])
		|| array_keys(@pvp['players']) == all_players()) {
			call_alias('/pvp start '.$id)
		}

	, 'warp',
		@arena = get_value('arena.'.$id)
		if(!@arena, die('Can\'t find that arena.'))
		if(array_contains_ic(array('outworld', 'outworld_nether', 'outworld_the_end'), pworld()), die('You cannot run this in the outworld.'))
		if(array_index_exists(@arena, 'lobby')) {
			set_ploc(@arena['lobby'])
		} else {
			msg('That arena isn\'t finished because it doesn\'t have a lobby set.')
		}

	, 'spectate',
		if(array_contains(get_scoreboards(), $id)) {
			set_pscoreboard(player(), $id)
		} else {
			msg('The arena isn\'t running.')
		}

	, 'start',
		@id = $id
		@pvp = import('pvp'.@id)
		if(!@pvp, die('There is no match to start.'))
		if(@pvp['running'] == true, die('Match already in progress.'))
		
		@pvp['arena'] = get_value('arena.'.@id)
		if(!@pvp['arena'], die('Can\'t find that arena.'))
		
		if(array_index_exists(@pvp['arena'], 'flags') && array_contains(@pvp['arena']['flags'], 'debug') && !array_contains(pgroup(), 'builders'), die('This arena can only be started by Builders.'))
		
		if((@pvp['cooldown'] + 15000) > time() && array_size(@pvp['players']) < @pvp['arena']['max'], die('Arena on 15 second cooldown.'))

		# CHECK ARENA SETTINGS
		if(!array_index_exists(@pvp['arena'], 'lobby'), die('No lobby defined for arena.'))
		if(!array_index_exists(@pvp['arena'], 'region'), die('No region defined for arena.'))
		if(!array_index_exists(@pvp['arena'], 'spawn'), die('No spawns defined for arena.'))
		if(!array_index_exists(@pvp['arena'], 'min'), @pvp['arena']['min'] = 2)
		if(!array_index_exists(@pvp['arena'], 'broadcast'), @pvp['arena']['broadcast'] = @pvp['arena']['region'])
		if(!array_index_exists(@pvp['arena'], 'mode'), @pvp['arena']['mode'] = 'dm')
		if(!array_index_exists(@pvp['arena'], 'ff'), @pvp['arena']['ff'] = true)
		if(!array_index_exists(@pvp['arena'], 'flags'), @pvp['arena']['flags'] = array())
		if(!array_index_exists(@pvp['arena'], 'team')) {
			@pvp['arena']['team'] = array(
				array('name': 'Team 1', 'color': 'c'), 
				array('name': 'Team 2', 'color': '3')
			)
		}
		if(!array_index_exists(@pvp['arena'], 'score')) {
			switch(@pvp['arena']['mode']
			, 'ctf', @pvp['arena']['score'] = 5
			, 'koth', @pvp['arena']['score'] = 60
			)
		}
		
		foreach(array_keys(@pvp['players']), @player,
			if(!ponline(@player) || pworld(@player) != 'custom') {
				array_remove(@pvp['players'], @player)
			}
		)
		if(array_size(@pvp['players']) < @pvp['arena']['min'], die('There aren\'t enough players. ('.array_size(@pvp['players']).')'))
		
		if(array_contains(get_scoreboards(), @id), remove_scoreboard(@id))
		create_scoreboard(@id)
		
		# PREPARE TEAMS
		if(array_contains(array('tdm', 'ctf'), @pvp['arena']['mode'])) {
			@pvp['team'] = array(array('players': array()), array('players': array()))
			if(array_index_exists(@pvp['arena'], 'teamratio')) {
				@upperteam = array_index(@pvp['arena']['teamratio'], max(@pvp['arena']['teamratio'][0], @pvp['arena']['teamratio'][1]))
				@lowerteam = array_index(@pvp['arena']['teamratio'], min(@pvp['arena']['teamratio'][0], @pvp['arena']['teamratio'][1]))
				@ratio = @pvp['arena']['teamratio'][@lowerteam] / @pvp['arena']['teamratio'][@upperteam]
			} else {
				@upperteam = 1
				@lowerteam = 0
				@ratio = 0.5
			}
			if(@pvp['arena']['mode'] == 'ctf') {
				@pvp['team'][0]['score'] = 0
				@pvp['team'][0]['flag'] = 0
				@pvp['team'][1]['score'] = 0
				@pvp['team'][1]['flag'] = 0
			}
			if(array_contains(@pvp['arena']['flags'], 'infection')) {
				@pvp['infectionteam'] = @lowerteam
			}
			create_team(@pvp['arena']['team'][0]['name'], @id)
			create_team(@pvp['arena']['team'][1]['name'], @id)
			set_team_display(@pvp['arena']['team'][0]['name'], array('prefix': color(@pvp['arena']['team'][0]['color'])), @id)
			set_team_display(@pvp['arena']['team'][1]['name'], array('prefix': color(@pvp['arena']['team'][1]['color'])), @id)
			set_team_options(@pvp['arena']['team'][0]['name'], array('friendlyinvisibles': true), @id)
			set_team_options(@pvp['arena']['team'][1]['name'], array('friendlyinvisibles': true), @id)
		}

		switch(@pvp['arena']['mode']
		, 'koth',
			create_objective('seconds', 'DUMMY', @id)
			set_objective_display('seconds', array('displayname': color('a').'Seconds Left', 'slot': 'SIDEBAR'), @id)
		, 'dm',
			if(array_index_exists(@pvp['arena'], 'lives')) {
				create_objective('lives', 'DUMMY', @id)
				if(@pvp['arena']['lives'] > 1) {
					set_objective_display('lives', array('displayname': color('a').'Lives Left', 'slot': 'SIDEBAR'), @id)
				}
			}
		, 'ctf',
			create_objective('captures', 'DUMMY', @id)
			set_objective_display('captures', array('displayname': color('a').'Captures ('.@pvp['arena']['score'].')', 'slot': 'SIDEBAR'), @id)
			team_add_player(@pvp['arena']['team'][0]['name'], @pvp['arena']['team'][0]['name'], @id)
			set_pscore('captures', @pvp['arena']['team'][0]['name'], 0, @id)
			team_add_player(@pvp['arena']['team'][1]['name'], @pvp['arena']['team'][1]['name'], @id)
			set_pscore('captures', @pvp['arena']['team'][1]['name'], 0, @id)
		)

		foreach(array_keys(@pvp['players']), @player,
			set_pscoreboard(@player, @id)
			
			if(array_contains(array('ctf', 'tdm'), @pvp['arena']['mode']),
				if(array_size(@pvp['team'][@upperteam]['players']) >= ceil((1 - @ratio) * array_size(@pvp['players']))) {
					@team = @lowerteam
				} else if(array_size(@pvp['team'][@lowerteam]['players']) >= ceil(@ratio * array_size(@pvp['players']))) {
					@team = @upperteam
				} else {
					@team = if(rand(100) < (@ratio * 100), @lowerteam, @upperteam)
				}
				@pvp['players'][@player]['team'] = @team
				array_push(@pvp['team'][@team]['players'], @player)
			)
			
			if(pmode(@player) == 'CREATIVE', set_pmode(@player, 'SURVIVAL'))
		)

		if(array_contains(array('tdm', 'ctf'), @pvp['arena']['mode']) && array_size(@pvp['team'][@lowerteam]['players']) < 1) {
			@pvp['players'][@player]['team'] = @lowerteam
			array_remove_values(@pvp['team'][@team]['players'], @player)
			array_push(@pvp['team'][@lowerteam]['players'], @player)
		}

		@pvp['coins'] = array_size(@pvp['players']) - 2
		@pvp['binds'] = array()
		@pvp['running'] = true

		#ANNOUNCE
		_worldmsg('custom', color('gray').'[PVP] '.color('r').'STARTING '.to_upper(@id).'...')
		if(array_contains(array('tdm', 'ctf'), @pvp['arena']['mode'])) {
			_regionmsg(@pvp['arena']['broadcast'], 
			color(@pvp['arena']['team'][0]['color']).'['.@pvp['arena']['team'][0]['name'].'] '.array_implode(@pvp['team'][0]['players'], ' ').'\n'.color('r').color(@pvp['arena']['team'][1]['color']).'['.@pvp['arena']['team'][1]['name'].'] '.array_implode(@pvp['team'][1]['players'], ' '))
		} else {
			_regionmsg(@pvp['arena']['broadcast'], array_implode(array_keys(@pvp['players']), ' vs '))
		}
		export('pvp'.@id, @pvp)
		
		#START ROUND
		set_timeout(3000, closure(
			include('pvp.ms')
			_start_round($id)
		))

	,
		msg('/pvp join <id> '.color('gray').'Join the match\n'
		.color('r').'/pvp start <id> '.color('gray').'Start the match\n'
		.color('r').'/pvp warp <id> '.color('gray').'Teleports you to the lobby\n'
		.color('r').'/pvp spectate <id> '.color('gray').'Displays the scoreboard')
	)
<<<	