*:/mail [$action] [$id] [$] = >>>
	switch($action
	, 'send',
		if(!$id || !$, die('Usage: /mail send <player> <message>'))
		@pdata = get_value('player.'.to_lower($id))
		if(!@pdata, die(color('red').'Unknown player. Make sure to use their full name.'))
		if(!array_index_exists(@pdata, 'mail'), @pdata['mail'] = array())
		array_push(@pdata['mail'], array(
			simple_date('yyyy-MM-dd h:mm a', time(), 'CST'),
			player(),
			$
		))
		store_value('player.'.to_lower($id), @pdata)
		msg(color('green').'Mail sent to '.$id)
	
	, 'read',
		@pdata = get_value('player.'.to_lower(player()))
		if(!array_index_exists(@pdata, 'mail'), die(color('gold').'You have no mail.'))
		if($id) {
			msg(color('yellow').'Sent from '.@pdata['mail'][$id][1].' on '.@pdata['mail'][$id][0])
			msg(colorize(@pdata['mail'][$id][2]))
			array_remove(@pdata['mail'], $id)
			if(array_size(@pdata['mail']) > 0) {
				msg(color('gold').'Remaining mail: '.array_size(@pdata['mail']))
			} else if(array_size(@pdata['mail']) == 0) {
				array_remove(@pdata, 'mail')
			}
			store_value('player.'.to_lower(player()), @pdata)
		} else {
			foreach(@pdata['mail'], @id, @mail,
				msg(color('gray').@id.color('yellow').' - '.@mail[0].' '.@mail[1].' '.color('r').substr(@mail[2], 0, min(length(@mail[2]), 16)).' '.color('gold').'[...]')
			)
		}
	
	, 
		die('/mail send <player> <message> '.color('gray').'Send a player an offline message.\n'
		.color('r').'/mail read [id] '.color('gray').'Read a list or a specific message.')
	)
<<<