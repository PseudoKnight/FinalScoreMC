/s $name $ = >>>
	broadcast(color('8').simple_date('h:mm').color('gray').' [console] '.color('aqua').$name.color('gray').': '.color('r').$) 
<<<

*:/say $ = >>>
	broadcast(color('8').simple_date('h:mm').' '.if(player() != '~console' && array_contains(pgroup(), 'donors'), color('green').'\u2666 ')._colorname().player().color('gray').': '.color('r').$) 
<<<

/sayas $from $to $ = >>>
	tmsg($to, color('gray').$from.': '.color('r').colorize($))
<<<

*:/me $ = >>>
	@ignorelist = import('ignorelist')
	if(array_index_exists(@ignorelist, player()) && array_contains(@ignorelist[player()], 'all')) {
		msg('You have been muted.')
		die()
	}
	broadcast('* '.player().' '.colorize($))
<<<

# MUTING
*:/ignore $player = >>>
	try(@player = player($player), die('No player found.'))
	@ignorelist = import('ignorelist')
	if(!array_index_exists(@ignorelist, @player)) {
		array_set(@ignorelist, @player, array())
	}
	if(array_contains_ic(@ignorelist[@player], player()), die('You are already ignoring '.@player.'.'))
	array_push(@ignorelist[@player], player())
	msg('You are now ignoring '.@player.'.')
	export('ignorelist', @ignorelist)
<<<

*:/unignore $player = >>>
	try(@player = player($player), die('No player found.'))
	@ignorelist = import('ignorelist')
	if(!array_index_exists(@ignorelist, @player), die('That player wasn\'t ignored by anyone.'))
	if(!array_contains_ic(@ignorelist[@player], player()), die('You have not ignored that player.'))
	for(@i = 0, @i < array_size(@ignorelist[@player]), @i++,
		if(@ignorelist[@player][@i] == player()) {
			array_remove(@ignorelist[@player], @i)
			msg('You are no longer ignoring '.@player.'.')
			break()
		}
	)
	export('ignorelist', @ignorelist)
<<<

*:/mute $player = >>>
	if(!has_permission('group.moderators'), die('You do not have permission to use this command.'))
	try(@player = player($player), die('No player found.'))
	@ignorelist = import('ignorelist')
	if(!array_index_exists(@ignorelist, @player)) {
		array_set(@ignorelist, @player, array())
	}
	if(array_contains(@ignorelist[@player], 'all'), die('Already muted.'))
	array_push(@ignorelist[@player], 'all')
	msg(@player.' is now muted.')
	export('ignorelist', @ignorelist)
<<<

*:/unmute $player = >>>
	if(!has_permission('group.moderators'), die('You do not have permission to use this command.'))
	try(@player = player($player), die('No player found.'))
	@ignorelist = import('ignorelist')
	if(!array_index_exists(@ignorelist, @player) || !array_contains(@ignorelist[@player], 'all'), die('That player isn\'t muted.'))
	for(@i = 0, @i < array_size(@ignorelist[@player]), @i++,
		if(@ignorelist[@player][@i] == 'all') {
			array_remove(@ignorelist[@player], @i)
			msg(@player.' is no longer muted.')
			break()
		}
	)
	export('ignorelist', @ignorelist)
<<<

# MESSAGING #
*:/msg $player $ = _pmsg($player, $)
*:/tell $player $ = _pmsg($player, $)
*:/w $player $ = _pmsg($player, $)

*:/r $ = >>>
	@conv = import('conv')
	try(@player = @conv[player()], die('You do not have any current conversations.'))
	if(@player != '~console' && !ponline(@player), die('That player is no longer online.'))
	_pmsg(@player, $)
<<<