*:/frogware [$cmd] = >>>
	switch($cmd
	, 'join',
		msg('NOTE: This is an alpha version.')
		if(!sk_region_exists('custom', 'frogware'), die('Define the frogware region first.'))
		include('frogware.ms')
		if(!array_contains(get_scoreboards(), 'fw')) {
			create_scoreboard('fw')
			create_objective('score', 'DUMMY', 'fw')
			create_team('losers', 'fw')
			create_team('winners', 'fw')
			set_objective_display('score', array('slot': 'SIDEBAR', 'displayname': color('a').color('l').'Get Ready!'), 'fw')
			set_team_display('winners', array('prefix': color('a')), 'fw')
			set_team_display('losers', array('prefix': color('f')), 'fw')
		}
		set_pscoreboard(player(), 'fw')
		team_add_player('losers', player(), 'fw')
		@score = 0
		foreach(all_players('custom'), @p,
			if(_fw_player(@p)
			&& get_pscore('score', @p, 'fw') > @score) {
				@score = get_pscore('score', @p, 'fw') - 1
			}
		)
		set_pscore('score', player(), @score, 'fw')
		set_ploc(_fw_loc(-1))
		if(pmode() == 'CREATIVE', set_pmode('SURVIVAL'))
		set_phunger(20)
		set_psaturation(5)
		set_phealth(20)
		_clear_pinv(player())
		
	, 'start',
		if(get_pscoreboard(player()) != 'fw', die('You haven\'t joined first.'))
		include('frogware.ms')
		foreach(all_players('custom'), @p,
			if(_fw_player(@p)) {
				set_phunger(@p, 20)
				set_psaturation(@p, 5)
				set_phealth(@p, 20)
				_clear_pinv(@p)
			}
		)
		_fw_startgame()
	
	, 'forcestop',
		queue_clear('fw')
		queue_clear('fw2')
		queue_clear('fw3')
		remove_scoreboard('fw')
		unbind('fwdamage')
	
	, 
		msg('| FrogWare is a game inspired by GarryWare,\n'
		.'| which is in turn inspired by WarioWare.\n'
		.'| Players get a random task every round that\n'
		.'| they have to complete in several seconds.\n'
		.'| They get a point for completing a task.\n'
		.'| First player to reach 25 points wins.')
	)
<<<