*:/frogware [$cmd] = >>>
	switch($cmd
	, 'join',
		msg('NOTE: This is an alpha version.')
		if(!sk_region_exists('custom', 'frogware'), die('Define the frogware region first.'))
		include('frogware.ms')
		if(!array_contains(get_scoreboards(), 'fw')) {
			create_scoreboard('fw')
			create_objective('score', 'DUMMY', 'fw')
			create_team('players', 'fw')
			set_objective_display('score', array('slot': 'SIDEBAR', 'displayname': color('a').color('l').'Get Ready!'), 'fw')
			set_team_options('players', array('friendlyinvisibles': false), 'fw')
		}
		set_pscoreboard(player(), 'fw')
		team_add_player('players', player(), 'fw')
		set_ploc(_fw_loc(-1))
		if(pmode() == 'CREATIVE', set_pmode('SURVIVAL'))
		
	, 'start',
		if(get_pscoreboard(player()) != 'fw', die('You haven\'t joined first.'))
		foreach(get_teams('fw')[0]['players'], @p,
			set_pscore('score', @p, 0, 'fw')
		)
		include('frogware.ms')
		_fw_startgame()
	
	, 'forcestop',
		queue_clear('fw')
		remove_scoreboard('fw')
		unbind('fwdamage')
	
	, 
		msg('| FrogWare is a game inspired by GarryWare,\n'
		.'| which is in turn inspired by WarioWare.\n'
		.'| Players get a random task every round that\n'
		.'| they have to complete in several seconds.\n'
		.'| They get a point for completing a task.\n'
		.'| First player to reach 20 points wins.')
	)
<<<